*Create output file

SET CATEGORY /COSTS,INCOME,PROFIT/;
SET SUBCATEGORY /
GENERATION_CAPITAL_COSTS, GENERATION_FIXED_COSTS, GENERATION_OPERATIONAL_COSTS, GENERATION_FUEL_COSTS,GENERATION_HYDRO,
TRANSMISSION_CAPITAL_COSTS, TRANSMISSION_OPERATIONAL_COSTS,
GENERATION_CO2_TAX,GENERATION_OTHER_EMI_TAX
ELECTRICITY_SALE,DISTRICT_HEAT_SALE,OTHER_TRADE_COSTS,OTHER_TRADE_INCOME,TOTAL_PROFIT,ENERGY_SPECIFIC_PROFIT
/;
SET COMMODITY /ELECTRICITY,DISTRICT_HEAT/;
SET TECH_TYPE /CONDENSING,CHP-BACK-PRESSURE,CHP-EXTRACTION,BOILERS,ELECT-TO-HEAT,INTERSEASONAL-HEAT-STORAGE,INTERSEASONAL-ELECT-STORAGE,INTRASEASONAL-HEAT-STORAGE,INTRASEASONAL-ELECT-STORAGE,HYDRO-RESERVOIRS,HYDRO-RUN-OF-RIVER,WIND-ON,WIND-OFF,SOLAR-PV,SOLAR-HEATING,HYDRO-WAVE,HEAT-PUMP,EL-BOILER,FUELCELL,ELECTROLYZER,H2-STORAGE,BIOMETH-DAC,HUB-OFF/;
SET GTECH_TYPE(GGG,TECH_TYPE);
SET PRICE_CATEGORY /AVERAGE,AVERAGE_WEIGHTED_BY_CONSUMPTION,AVERAGE_WEIGHTED_BY_PRODUCTION/;
SET VARIABLE_CATEGORY /EXOGENOUS,ENDOGENOUS,DECOMMISSIONING,EXOGENOUS_AC,STORAGE,P2H,HYDROGEN/;
SET UNITS /GW,TWh,GWh,MWh,Money_per_MWh,Mmoney,kton/;


   GTECH_TYPE(G,'CONDENSING')$IGCND(G)                                                    = YES;
   GTECH_TYPE(G,'CHP-BACK-PRESSURE')$IGBPR(G)                                             = YES;
   GTECH_TYPE(G,'CHP-EXTRACTION')$IGEXT(G)                                                = YES;
   GTECH_TYPE(G,'BOILERS')$IGHOB(G)                                                       = YES;
   GTECH_TYPE(G,'ELECT-TO-HEAT')$IGETOH(G)                                                = YES;
   GTECH_TYPE(G,'INTRASEASONAL-HEAT-STORAGE')$(GDATA(G,'GDTYPE') EQ GHSTO)                = YES;
   GTECH_TYPE(G,'INTERSEASONAL-HEAT-STORAGE')$(GDATA(G,'GDTYPE') EQ GHSTOS)               = YES;
   GTECH_TYPE(G,'INTRASEASONAL-ELECT-STORAGE')$(GDATA(G,'GDTYPE') EQ GESTO)               = YES;
   GTECH_TYPE(G,'INTERSEASONAL-ELECT-STORAGE')$(GDATA(G,'GDTYPE') EQ GESTOS)              = YES;
   GTECH_TYPE(G,'HYDRO-RESERVOIRS')$IGHYRS(G)                                             = YES;
   GTECH_TYPE(G,'HYDRO-RUN-OF-RIVER')$IGHYRR(G)                                           = YES;
   GTECH_TYPE(G,'WIND-ON')$(IGWND(G) AND GDATA(G,'GDTECHGROUP')  EQ WINDTURBINE_ONSHORE)  = YES;
   GTECH_TYPE(G,'WIND-OFF')$(IGWND(G) AND GDATA(G,'GDTECHGROUP') EQ WINDTURBINE_OFFSHORE) = YES;
   GTECH_TYPE(G,'SOLAR-PV')$IGSOLE(G)                                                     = YES;
   GTECH_TYPE(G,'SOLAR-HEATING')$IGSOLH(G)                                                = YES;
   GTECH_TYPE(G,'HYDRO-WAVE')$IGWAVE(G)                                                   = YES;
*$ifi %HYDROGEN%==yes GTECH_TYPE(G,'FUELCELL')$(GDATA(G,'GDTECHGROUP') EQ FUELCELL)                = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'FUELCELL')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GH2TOE)               = YES;
*$ifi %HYDROGEN%==yes GTECH_TYPE(G,'ELECTROLYZER')$(GDATA(G,'GDTECHGROUP') EQ ELECTROLYZER)        = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'ELECTROLYZER')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GETOH2 OR GDATA(G,'GDTYPE') EQ HYDROGEN_GEHTOH2)        = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'H2-STORAGE')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GH2STO)            = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'BIOMETH-DAC')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GH2TOBIOMETH)     = YES;
$ifi %OFFSHOREGRID%==yes GTECH_TYPE(G,'HUB-OFF')$(GDATA(G,'GDTYPE') EQ HUB_OFF)     = YES;


PARAMETER G_CAP_YCRAF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS) "Generation capacity for each year, country, region, area, technology, fuel, commodity, technology type and variable type (GW)";
PARAMETER G_STO_YCRAF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS) "Generation storage for each year, country, region, area, technology, fuel, commodity, technology type and variable type (GWh)";
PARAMETER PRO_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Energy Production for each year, country, region, area, technology, fuel, season, hour, commodity and technology type (MWh)" ;
PARAMETER PRO_YCRAFST(Y,C,RRR,AAA,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Energy Production for each year, country, region, area, fuel, season, hour, commodity and technology type (MWh)" ;
PARAMETER PRO_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS) "Annual Energy Production for each year, country, region, area, technology, fuel, commodity and technology type (TWh)";
PARAMETER F_CONS_YCRAST(Y,C,RRR,AAA,G,FFF,SSS,TTT,TECH_TYPE,UNITS) "Fuel consumption for each year, country, region, area, technology, fuel, season, hour and technology type (MWh)";
PARAMETER F_CONS_YCRA(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS) "Fuel consumption for each year, country, region, area, technology, fuel and technology type (TWh)";
PARAMETER X_CAP_YCR(Y,C,IRRRE,IRRRI,VARIABLE_CATEGORY,UNITS) "Transmission capacity for each year, country, from region to region (GW)";
PARAMETER X_FLOW_YCRST(Y,C,IRRRE,IRRRI,SSS,TTT,UNITS) "Transmission flow for each year, country, from region to region, for each season and hour (MWh)";
PARAMETER X_FLOW_YCR(Y,C,IRRRE,IRRRI,UNITS) "Transmission flow for each year, country, from region to region (TWh)";
PARAMETER ECO_G_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,CATEGORY,SUBCATEGORY,UNITS) "Generation Economic Output for each year, country, region, area and technology (Mmoney)";
PARAMETER ECO_X_YCR(Y,C,RRR,CATEGORY,SUBCATEGORY,UNITS) "Electric Transmission Economic Output for each year, country and region (Mmoney)";
PARAMETER OBJ_YCR(Y,C,RRR,SUBCATEGORY,UNITS) "Objective function (Mmoney)";
PARAMETER EL_PRICE_YCR(Y,C,RRR,PRICE_CATEGORY,UNITS) "Average Electricity Prices for each region (money/MWh)";
PARAMETER EL_PRICE_YCRST(Y,C,RRR,SSS,TTT,UNITS) "Hourly Electricity Prices for each region (money/MWh)";
PARAMETER DH_PRICE_YCRA(Y,C,RRR,AAA,PRICE_CATEGORY,UNITS) "Average District Heating Prices for each area(money/MWh)";
PARAMETER DH_PRICE_YCRAST(Y,C,RRR,AAA,SSS,TTT,UNITS) "Hourly District Heating Prices for each area (money(MWh)";
PARAMETER EL_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,UNITS) "Aggregated Hourly Electricity Demand (MWh)";
PARAMETER EL_DEMAND_YCR(Y,C,RRR,VARIABLE_CATEGORY,UNITS) "Aggregated annual Electricity Demand (TWh)";
PARAMETER EL_NET_DEMAND_YCRST(Y,C,RRR,SSS,TTT,DEUSER,UNITS) "Net electricity Demand (MWh)";
PARAMETER DH_DEMAND_YCRAST(Y,C,RRR,AAA,SSS,TTT,VARIABLE_CATEGORY,UNITS) "Aggregated Hourly DH Demand (MWh)";
PARAMETER DH_DEMAND_YCRA(Y,C,RRR,AAA,VARIABLE_CATEGORY,UNITS) "Aggregated Annual DH Demand (MWh)";
PARAMETER EMI_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS) "Annual CO2 emissions(ktons)";
PARAMETER CURT_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Hourly energy curtailment per country, region, area, technology, fuel, hour and technology type (MWh)";
PARAMETER CURT_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS) "Hourly energy curtailment per country, region, area, technology, fuel, hour and technology type (MWh)";
PARAMETER WEEKLY_HYDRO_STORAGE_VOLUME(Y,C,RRR,AAA,SSS,UNITS);



*Modification of IHOURSINST
$ifi %RollingSeasons%==yes PARAMETER IHOURSINTROLLINGSEASON(S,T) "Length of time segment (hours) used in rolling season mode";
$ifi %RollingSeasons%==yes IWEIGHSUMS=SUM(SSS$(ORD(SSS) LE ROLLINGSEASONSNUMBER), WEIGHT_S(SSS));
$ifi %RollingSeasons%==yes IHOURSINTROLLINGSEASON(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*SUM(ITALIAS, WEIGHT_T(ITALIAS)));

IWEIGHSUMS = SUM(S, WEIGHT_S(S));
IWEIGHSUMT = SUM(T, WEIGHT_T(T));
IHOURSINST(SSS,TTT)=0;
IHOURSINST(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*IWEIGHSUMT);
ALIAS(C,ALIAS_C);

* -------------------- ENERGY CAPACITY ---------------------

* ------------- ELECTRICITY CAPACITY --------------

** EXOGENOUS ELECTRICITY CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) )=
IOF0001*GKFX(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;


** ENDOGENOUS ELECTRIC CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) )=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;

** ENDOGENOUS ELECTRICITY CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) )=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;


* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT CAPACITY --------------

** EXOGENOUS HEAT CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) )=
IOF0001*(GKFX(Y,IA,IGH)/(1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH))))
;


** ENDOGENOUS HEAT CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) )=
IOF0001*(VGKNACCUMNET.L(Y,IA,IGH)/(1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH))))
;

** ENDOGENOUS HEAT CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) )=
$ifi %DECOM%==yes -IOF0001*(VDECOM_EXO_ACCUM.L(Y,IA,IGH)/(1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH))))
;

* ------------- END OF HEAT CAPACITY --------------

* ---------------- END OF ENERGY CAPACITY -----------------

* -------------------- ENERGY STORAGE ---------------------

* ------------- ELECTRICITY STORAGE --------------

** EXOGENOUS ELECTRICITY STORAGE

G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE) OR IGHYRS(IGE)) )=
IOF0001*GKFX(Y,IA,IGE)*(1$(NOT IGHYRS(IGE)) + HYRSMAXVOL_G(IA,IGE)$IGHYRS(IGE))
;

** ENDOGENOUS ELECTRIC STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE) OR IGHYRS(IGE)) )=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)*(1$(NOT IGHYRS(IGE)) + HYRSMAXVOL_G(IA,IGE)$IGHYRS(IGE))
;

** ENDOGENOUS ELECTRICITY STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) AND (IGESTO(IGE) OR IGESTOS(IGE) OR IGHYRS(IGE)) )=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)*(1$(NOT IGHYRS(IGE)) + HYRSMAXVOL_G(IA,IGE)$IGHYRS(IGE))
;

* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT STORAGE --------------

** EXOGENOUS HEAT STORAGE

G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
IOF0001*GKFX(Y,IA,IGH)
;

** ENDOGENOUS HEAT STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGH)
;

** ENDOGENOUS HEAT STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)
;

* ------------- END OF HEAT STORAGE --------------
* -------------------- ENERGY PRODUCTION ---------------------

** ELECTRICITY PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
       VGE_T.L(Y,IA,IGE,S,T);

** ELECTRICITY PRODUCTION ANNUALLY
PRO_YCRAGF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'TWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T))/IOF1000000;

** DISTRICT HEAT PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'DISTRICT_HEAT',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
       VGH_T.L(Y,IA,IGH,S,T);

** DISTRICT HEAT PRODUCTION ANNUALY
PRO_YCRAGF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T))/IOF1000000;


** ENERGY PRODUCTION HOURLY
PRO_YCRAFST(Y,C,IR,IA,FFF,S,T,COMMODITY,TECH_TYPE,'MWh') =  SUM(G$IAGK_HASORPOT(Y,IA,G),PRO_YCRAGFST(Y,C,IR,IA,G,FFF,S,T,COMMODITY,TECH_TYPE,'MWh'));


* ---------------- END OF ENERGY PRODUCTION -----------------

* ---------------- STORAGE VOLUME -----------------

WEEKLY_HYDRO_STORAGE_VOLUME(Y,C,IR,IA,S,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=VHYRS_S.L(Y,IA,S)
$ifi %hydrocas_FLH%==yes  + SUM(G$IGHYRS(G), VHYDROCAS_RESLEVEL.L(Y,IA,G,S))
;
* ---------------- END OF STORAGE VOLUME -----------------
* -------------------- FUEL CONSUMPTION ---------------------

** HOURLY FUEL CONSUMPTION
F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       (VGE_T.L(Y,IA,G,S,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,S,T)*IGFH_FACTOR(Y,IA,G));

** ANNUAL FUEL CONSUMPTION
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*(VGE_T.L(Y,IA,G,S,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,S,T)*IGFH_FACTOR(Y,IA,G)))/IOF1000000;


* ---------------- END OF FUEL CONSUMPTION -----------------

* ---------------- ENERGY CURTAILMENT -----------------------
*HOURLY CURTAILMENT
CURT_YCRAGFST(Y,C,IR,IA,IGHYRR,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRR) AND IGF(IGHYRR,FFF) AND GTECH_TYPE(IGHYRR,'HYDRO-RUN-OF-RIVER') AND IWTRRRSUM(IA))=
(WTRRRFLH(IA) * (
           GKFX(Y,IA,IGHYRR)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGHYRR)
        +  VGKNACCUMNET.L(Y,IA,IGHYRR)
                          ) * WTRRRVAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGHYRR,S,T)) + IGKRATE(IA,IGHYRR,S,T))) / IWTRRRSUM(IA)
-VGE_T.L(Y,IA,IGHYRR,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-ON','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-ON')  AND IWND_SUMST(IA)
$ifi %STEPWISEKPOT%==YES       AND NOT IKPOTSTEP(IA,IGWND)
                 )=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
            GKFX(Y,IA,IGWND)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
         + VGKNACCUMNET.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-OFF') AND IWND_SUMST(IA)
$ifi %STEPWISEKPOT%==YES       AND NOT IKPOTSTEP(IA,IGWND)
                 )=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
            GKFX(Y,IA,IGWND)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
         + VGKNACCUMNET.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,'SOLAR-PV') AND ISOLESUMST(IA)
$ifi %STEPWISEKPOT%==YES       AND NOT IKPOTSTEP(IA,IGSOLE)
                 )=
$ifi %SOLEFLH_DOL%==AAA         (SOLEFLH(IA) * (
$ifi %SOLEFLH_DOL%==AAA_GGG     (SOLEFLH(IA,IGSOLE) * (
            GKFX(Y,IA,IGSOLE)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLE)
         + VGKNACCUMNET.L(Y,IA,IGSOLE)
                 )  * SOLE_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLE,S,T)) + IGKRATE(IA,IGSOLE,S,T))) / ISOLESUMST(IA)
-VGE_T.L(Y,IA,IGSOLE,S,T);

$ifi NOT %STEPWISEKPOT%==YES $goto endstepwisekpot
CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,TECH_TYPE) AND IWND_SUMST(IA)
              AND IKPOTSTEP(IA,IGWND)
                 )=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
                               SUM(KPOTSTEP,(IGKFX_KPOTSTEP(Y,IA,IGWND,KPOTSTEP) + VGKNACCUMNET_KPOTSTEP.L(Y,IA,IGWND,KPOTSTEP)) *IKPOTSTEP_FLHA(IA,IGWND,KPOTSTEP))
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,TECH_TYPE) AND ISOLESUMST(IA)
              AND IKPOTSTEP(IA,IGSOLE)
                 )=
$ifi %SOLEFLH_DOL%==AAA         (SOLEFLH(IA) * (
$ifi %SOLEFLH_DOL%==AAA_GGG     (SOLEFLH(IA,IGSOLE) * (
                               SUM(KPOTSTEP,(IGKFX_KPOTSTEP(Y,IA,IGSOLE,KPOTSTEP) + VGKNACCUMNET_KPOTSTEP.L(Y,IA,IGSOLE,KPOTSTEP)) *IKPOTSTEP_FLHA(IA,IGSOLE,KPOTSTEP))
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLE)
                 )  * SOLE_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLE,S,T)) + IGKRATE(IA,IGSOLE,S,T))) / ISOLESUMST(IA)
-VGE_T.L(Y,IA,IGSOLE,S,T);
$label endstepwisekpot

CURT_YCRAGFST(Y,C,IR,IA,IGSOLH,FFF,S,T,'DISTRICT_HEAT','SOLAR-HEATING','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLH) AND IGF(IGSOLH,FFF) AND GTECH_TYPE(IGSOLH,'SOLAR-HEATING') AND ISOLHSUMST(IA))=
$ifi %SOLHFLH_DOL%==AAA         (SOLHFLH(IA) * (
$ifi %SOLHFLH_DOL%==AAA_GGG     (SOLHFLH(IA,IGSOLH) * (
            GKFX(Y,IA,IGSOLH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLH)
         + VGKNACCUMNET.L(Y,IA,IGSOLH)
                 )  * SOLH_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLH,S,T)) + IGKRATE(IA,IGSOLH,S,T))) / ISOLHSUMST(IA)
-VGH_T.L(Y,IA,IGSOLH,S,T);

CURT_YCRAGFST(Y,C,IR,IA,G,FFF,S,T,COMMODITY,TECH_TYPE,UNITS)$(ABS(CURT_YCRAGFST(Y,C,IR,IA,G,FFF,S,T,COMMODITY,TECH_TYPE,UNITS)) LT 0.000001)  =0;

*ANNUAL CURTAILMENT
CURT_YCRAGF(Y,C,IR,IA,IGHYRR,FFF,'ELECTRICITY','HYDRO-RUN-OF-RIVER','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRR) AND IGF(IGHYRR,FFF) AND GTECH_TYPE(IGHYRR,'HYDRO-RUN-OF-RIVER'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGHYRR,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGWND,FFF,'ELECTRICITY','WIND-ON','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-ON'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-ON','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGWND,FFF,'ELECTRICITY','WIND-OFF','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-OFF'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGSOLE,FFF,'ELECTRICITY','SOLAR-PV','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,'SOLAR-PV'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGSOLH,FFF,'DISTRICT_HEAT','SOLAR-HEATING','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLH) AND IGF(IGSOLH,FFF) AND GTECH_TYPE(IGSOLH,'SOLAR-HEATING'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGSOLH,FFF,S,T,'DISTRICT_HEAT','SOLAR-HEATING','MWh'))/IOF1000000
;

*Curtailment of hydro reservoirs should be analyzed by aggregating the technology set
CURT_YCRAGF(Y,C,IR,IA,IGHYRS,FFF,'ELECTRICITY','HYDRO-RESERVOIRS','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRS) AND IGF(IGHYRS,FFF) AND GTECH_TYPE(IGHYRS,'HYDRO-RESERVOIRS'))=
SUM(S,
        IHYINF_S(IA,S) *
        (
        GKFX(Y,IA,IGHYRS)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGHYRS)
       +VGKNACCUMNET.L(Y,IA,IGHYRS)
         )
         -SUM(T,IHYRST(S,T)*VGE_T.L(Y,IA,IGHYRS,S,T))
    )/IOF1000000
;


* ---------------- END OF ENERGY CURTAILMENT -----------------------

* ------------------------- ENERGY DEMAND ----------------------

* ------------- ELECTRICITY DEMAND --------------

*HOURLY
** EXOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh')$CCCRRR(C,IR)=
         SUM(DEUSER,IDE_T(Y,IR,DEUSER,S,T))/(1-DISLOSS_E(IR));

$ifi not %GRIDTARIFFS%==YES $ifi %TAXES%==YES EL_NET_DEMAND_YCRST(Y,C,IR,S,T,DEUSER,'MWh')$CCCRRR(C,IR)=  VDENET_T.L(Y,IR,DEUSER,S,T);
$ifi %GRIDTARIFFS%==YES $ifi not %TAXES%==YES EL_NET_DEMAND_YCRST(Y,C,IR,S,T,DEUSER,'MWh')$CCCRRR(C,IR)=  VDENET_T.L(Y,IR,DEUSER,S,T);
$ifi %GRIDTARIFFS%==YES $ifi %TAXES%==YES     EL_NET_DEMAND_YCRST(Y,C,IR,S,T,DEUSER,'MWh')$CCCRRR(C,IR)=  VDENET_T.L(Y,IR,DEUSER,S,T);

** ENDOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'P2H','MWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
         SUM(IA$RRRAAA(IR,IA), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)))
;


EL_DEMAND_YCRST(Y,C,IR,S,T,'STORAGE','MWh')$CCCRRR(C,IR)=

* INTRASEASONAL ELECTRICITY STORAGE
         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADT.L(Y,IA,IGESTO,S,T)))
* INTERSEASONAL ELECTRICITY STORAGE
         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),VESTOLOADTS.L(Y,IA,IGESTOS,S,T)))
;

$ifi %HYDROGEN%==YES EL_DEMAND_YCRST(Y,C,IR,S,T,'HYDROGEN','MWh')$CCCRRR(C,IR)=
* Electrolysers
$ifi %HYDROGEN%==YES         + SUM(IA$RRRAAA(IR,IA), SUM(IHYDROGEN_GETOH2$IAGK_HASORPOT(Y,IA,IHYDROGEN_GETOH2), VGE_T.L(Y,IA,IHYDROGEN_GETOH2,S,T)))
$ifi %HYDROGEN%==YES           + SUM(IA$RRRAAA(IR,IA), SUM(IHYDROGEN_GEHTOH2$IAGK_HASORPOT(Y,IA,IHYDROGEN_GEHTOH2), VGE_T.L(Y,IA,IHYDROGEN_GEHTOH2,S,T)))
;
*ANNUAL
EL_DEMAND_YCR(Y,C,IR,VARIABLE_CATEGORY,'TWh')$(CCCRRR(C,IR)) =  SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,VARIABLE_CATEGORY,'MWh'))/IOF1000000;


* PRO_YCRAGF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'TWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
*       SUM((S,T),IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T))/IOF1000000;


* ----------- END OF ELECTRICITY DEMAND --------------

* ------------- HEAT DEMAND --------------

** EXOGENOUS DEMAND

DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,'EXOGENOUS','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
         SUM(DHUSER,IDH_T(Y,IA,DHUSER,S,T))/(1-DISLOSS_H(IA));

** ENDOGENOUS DEMAND

DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDOGENOUS','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=

* INTRASEASONAL HEAT STORAGE
        SUM(IGHSTO$IAGK_HASORPOT(Y,IA,IGHSTO),VHSTOLOADT.L(Y,IA,IGHSTO,S,T))
* INTERSEASONAL HEAT STORAGE
        + SUM(IGHSTOS$IAGK_HASORPOT(Y,IA,IGHSTOS),VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T))
;

* ----------- END OF HEAT DEMAND --------------

* -------------------- END OF ENERGY DEMAND -----------------------



* -------------------- TRANSMISSION CAPACITY -----------------------

** EXOGENOUS ELECTRICITY CAPACITY

X_CAP_YCR(Y,C,IR,IRI,'EXOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=  IOF0001*XKFX(Y,IR,IRI);
$ifi %FBEU%==yes  X_CAP_YCR(Y,C,IR,IRI,'EXOGENOUS_AC','GW')$(CCCRRR(C,IR) AND SUM(ACNETWORKS, IXK_AC(ACNETWORKS,IR,IRI)))=  IOF0001*XKFX('2016',IR,IRI);


** ENDOGENOUS ELECTRIC CAPACITY

$ifi not %TransInvest%==yes $goto Transmission_investments_end
X_CAP_YCR(Y,C,IR,IRI,'ENDOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*VXKNACCUMNET.L(Y,IR,IRI)
;
$label  Transmission_investments_end


* -------------------- END OF TRANSMISSION CAPACITY -----------------------

* -------------------- TRANSMISSION FLOW -----------------------

** HOURLY TRANSMISSION FLOW

X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
VX_T.L(Y,IR,IRI,S,T)
$ifi %FBEU%==yes   + VX_AC_T.L(Y,IR,IRI,S,T)$SUM(ACNETWORKS,IXK_AC(ACNETWORKS,IR,IRI))
;


** ANNUAL TRANSMISSION FLOW
X_FLOW_YCR(Y,C,IR,IRI,'TWh')=
SUM((S,T),IHOURSINST(S,T)*X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh'))/IOF1000000;
;



* -------------------- END OF TRANSMISSION FLOW -----------------------



* ---------------------- ECONOMIC OUTPUT ----------------------

* ------------- COMMODITY PRICES --------------

** ELECTRICITY PRICES

*HOURLY ELECTRICITY PRICE

EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')$CCCRRR(C,IR)=OMONEY*QEEQ.M(Y,IR,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE ELECTRICITY PRICE

EL_PRICE_YCR(Y,C,IR,'AVERAGE','Money_per_MWh')$CCCRRR(C,IR)=
SUM((S,T),EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));
** DISTRICT HEATING PRICES

*HOURLY DISTRICT HEATING PRICE

DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=OMONEY*QHEQ.M(Y,IA,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE DISTRICT HEATING PRICE

DH_PRICE_YCRA(Y,C,IR,IA,'AVERAGE','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
SUM((S,T),DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));

** END OF DISTRICT HEATING PRICES
* ------------- END OF COMMODITY PRICES --------------
* ------------- COSTS --------------

* -- GENERATION COSTS -----

** GENERATION INVESTMENTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and RRRAAA(IR,IA))=
         OMONEY*
         (VGKN.L(Y,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G)*SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),IYHASANNUITYG(Y,IYALIAS2,G))
*investments in previous years
         + SUM(IYALIAS2$(ORD(IYALIAS2) LT ORD(Y)), VGKN.L(IYALIAS2,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G)*IYHASANNUITYG(IYALIAS2,Y,G))
         );

** GENEARTION FIXED COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FIXED_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000*GOMFCOST(IA,G)*(
         GKFX(Y,IA,G)
         +VGKNACCUMNET.L(Y,IA,G)
$ifi %DECOM%==yes         -VDECOM_EXO_ACCUM.L(Y,IA,G)$IGDECOMEXOPOT(Y,IA,G)
         );

** GENERATION O&M COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY/IOF1000000*
     ((GOMVCOSTO(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGE_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKE(G)

   +  (GOMVCOSTO(IA,G) * GDATA(G,'GDCV') *
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGH_T.L(Y,IA,G,S,T))))$IGKH(G)

   +  (GOMVCOSTIN(IA,G)*
      (
$ifi %VGF%==ST   +   SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T))))
$ifi %VGF%==Y   +   VGF_T.L(Y,IA,G)))
$ifi %VGF%==Y2   +   SUM((IS3,T), IHOURSINST(IS3,T)*(VGE_T.L(Y,IA,G,IS3,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,IS3,T)*IGFH_FACTOR(Y,IA,G)))))

     ) ;

** GENERATION FUEL COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FUEL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY*IOF3P6/IOF1000000*(
                 FUELPRICE(Y,IA,FFF)
$ifi not %STEPWISEPRICE%==yes $goto No_STEPWISEPRICE
                 + (SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),
                 STEPWISEPRICE_FUELPRICE_STEP(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)*VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
                 /SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))$(SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))
$label No_STEPWISEPRICE
                 )*
   (
$ifi %VGF%==ST                 SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T) )
$ifi %VGF%==Y                  VGF_T.L(Y,IA,G)
$ifi %VGF%==Y2                  SUM((IS3,T), IHOURSINST(IS3,T)*(VGE_T.L(Y,IA,G,IS3,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,IS3,T)*IGFH_FACTOR(Y,IA,G)))
    )$(IGNOTETOH(G) and (not IGESTO(G)) and (not IGHSTO(G)) and (not IGESTOS(G)) and (not IGHSTOS(G)))

$ONTEXT
*NOT RELEVANT
* Electricity storages:
    +OMONEY/IOF1000000*SUM((S,T), QEEQ.M(Y,IR,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)*VESTOLOADT.L(Y,IA,G,S,T))$IGESTO(G)
    +OMONEY/IOF1000000*SUM((S,T), QEEQ.M(Y,IR,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)*VESTOLOADTS.L(Y,IA,G,S,T))$IGESTOS(G)
* Heat storages:
    +OMONEY/IOF1000000*SUM((S,T), QHEQ.M(Y,IA,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)*VHSTOLOADT.L(Y,IA,G,S,T))$IGHSTO(G)
    +OMONEY/IOF1000000*SUM((S,T), QHEQ.M(Y,IA,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)*VHSTOLOADTS.L(Y,IA,G,S,T))$IGHSTOS(G)
* Electricity to heat:
    +OMONEY/IOF1000000*SUM((S,T), QEEQ.M(Y,IR,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)*VGE_T.L(Y,IA,G,S,T))$(IAGK_HASORPOT(Y,IA,G) and IGETOH(G))
$OFFTEXT
;

** Hydro with storage seasonal price profile

ECO_G_YCRAG(Y,C,IR,IA,IGHYRS,FFF,TECH_TYPE,'COSTS','GENERATION_HYDRO','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(IGHYRS,FFF) AND GTECH_TYPE(IGHYRS,TECH_TYPE) AND IAGK_HASORPOT(Y,IA,IGHYRS))=
   OMONEY/IOF1000000*
    SUM((S,T), HYPPROFILS(IA,S)* IHOURSINST(S,T)* VGE_T.L(Y,IA,IGHYRS,S,T));

** CO2 EMISSIONS COST

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TAX','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
$ifi %VGF%==ST         OMONEY/IOF1000000*IM_CO2(G)*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * SUM((S,T), IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T));
$ifi %VGF%==Y          OMONEY/IOF1000000*IM_CO2(G)*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * VGF_T.L(Y,IA,G);
$ifi %VGF%==Y2          OMONEY/IOF1000000*IM_CO2(G)*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * SUM((IS3,T), IHOURSINST(IS3,T)*(VGE_T.L(Y,IA,G,IS3,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,IS3,T)*IGFH_FACTOR(Y,IA,G)));

** OTHER EMISSIONS COST
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OTHER_EMI_TAX','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000000*(
$ifi %VGF%==ST          SUM((S,T), IHOURSINST(S,T) * (FDATA(FFF,'FDSO2')*IOF0001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))*M_POL(Y,"TAX_SO2",C)
$ifi %VGF%==ST          + SUM((S,T), IHOURSINST(S,T) * (GDATA(G,'GDNOX')*IOF0000001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))* M_POL(Y,"TAX_NOX",C)
$ifi %VGF%==Y           (FDATA(FFF,'FDSO2')*IOF0001) * IOF3P6 * VGF_T.L(Y,IA,G)*M_POL(Y,"TAX_SO2",C)
$ifi %VGF%==Y          +  (GDATA(G,'GDNOX')*IOF0000001) * IOF3P6 * VGF_T.L(Y,IA,G)* M_POL(Y,"TAX_NOX",C)
$ifi %VGF%==Y2           (FDATA(FFF,'FDSO2')*IOF0001) * IOF3P6 * SUM((IS3,T), IHOURSINST(IS3,T)*(VGE_T.L(Y,IA,G,IS3,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,IS3,T)*IGFH_FACTOR(Y,IA,G)))*M_POL(Y,"TAX_SO2",C)
$ifi %VGF%==Y2          +  (GDATA(G,'GDNOX')*IOF0000001) * IOF3P6 * SUM((IS3,T), IHOURSINST(IS3,T)*(VGE_T.L(Y,IA,G,IS3,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,IS3,T)*IGFH_FACTOR(Y,IA,G)))* M_POL(Y,"TAX_NOX",C)
          );

* -- TRANSMISSION COSTS -----
*ELECTRICITY
** TRANSMISSION INVESTMENTS COSTS
ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
          OMONEY/IOF1000000*(

* Investment costs, new transmission capacity
* (the average of the annuities for the two countries in question is used for international transmission):

     SUM(IRI$IXKN(Y,IRI,IR),IOF05*VXKN.L(Y,IR,IRI)*XINVCOST(Y,IR,IRI)*
      (IOF05* (ANNUITYCX(C)+SUM(ALIAS_C$CCCRRR(ALIAS_C,IRI),ANNUITYCX(ALIAS_C))))*SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),IYHASANNUITYX(IYALIAS2,Y)))

*Investment costs, transmission capacity invested in previous years
    + SUM((IYALIAS2,IRI)$(IXKN(IYALIAS2,IRI,IR) AND (ORD(IYALIAS2) LT ORD(Y))),IOF05*VXKN.L(IYALIAS2,IR,IRI)*XINVCOST(IYALIAS2,IR,IRI)*
      (IOF05* (ANNUITYCX(C)+SUM(ALIAS_C$CCCRRR(ALIAS_C,IRI),ANNUITYCX(ALIAS_C))))*IYHASANNUITYX(IYALIAS2,Y))
         );

** TRANSMISSION FLOW

ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$SUM((IRI,S,T),X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh'))=
OMONEY*IOF0000001*SUM(IRI, SUM((S,T), IHOURSINST(S,T) * (X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh') * XCOST(IR,IRI))));

** TRANSMISSION TRADE COST
ECO_X_YCR(Y,C,IR,'COSTS','OTHER_TRADE_COSTS','Mmoney')$SUM((IRI,S,T),X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh'))=
OMONEY*IOF0000001*SUM(IRI, SUM((S,T), IHOURSINST(S,T) * (X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh') * EL_PRICE_YCRST(Y,C,IRI,S,T,'Money_per_MWh'))));

** TRANSMISSION TRADE INCOME
ECO_X_YCR(Y,C,IR,'INCOME','OTHER_TRADE_INCOME','Mmoney')$SUM((IRI,S,T),X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh'))=
OMONEY*IOF0000001*SUM(IRI, SUM((S,T), IHOURSINST(S,T) * (X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh') * EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh'))));

* ------------- END OF COSTS --------------



* ------------- OBJECTIVE FUNCTION --------------

*GENERATION COSTS
OBJ_YCR(Y,C,IR,'GENERATION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=  SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney'));
OBJ_YCR(Y,C,IR,'GENERATION_FIXED_COSTS','Mmoney')$CCCRRR(C,IR)=  SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FIXED_COSTS','Mmoney'));
OBJ_YCR(Y,C,IR,'GENERATION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=  SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney'));
OBJ_YCR(Y,C,IR,'GENERATION_FUEL_COSTS','Mmoney')$CCCRRR(C,IR)=  SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FUEL_COSTS','Mmoney'));
OBJ_YCR(Y,C,IR,'GENERATION_HYDRO','Mmoney')$CCCRRR(C,IR)=  SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_HYDRO','Mmoney'));
OBJ_YCR(Y,C,IR,'GENERATION_CO2_TAX','Mmoney')$CCCRRR(C,IR)=  SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TAX','Mmoney'));
OBJ_YCR(Y,C,IR,'GENERATION_OTHER_EMI_TAX','Mmoney')$CCCRRR(C,IR)=  SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OTHER_EMI_TAX','Mmoney'));
*TRANSMISSION COSTS
OBJ_YCR(Y,C,IR,'TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=   ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney');
OBJ_YCR(Y,C,IR,'TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=   ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney');

* ------------- END OF OBJECTIVE FUNCTION --------------

* ------------- INCOME --------------

** ELECTRICITY SALES
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME','ELECTRICITY_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G) and IGE(G))=
 OMONEY*IOF0000001*
   (SUM((S,T), VGE_T.L(Y,IA,G,S,T)*QEEQ.M(Y,IR,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)))*(1-2$(IGETOH(G)));

* ------------- END OF INCOME --------------





* ------------- PROFIT --------------
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'PROFIT','TOTAL_PROFIT','Mmoney')$IAGK_HASORPOT(Y,IA,G)=
SUM(SUBCATEGORY, ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME',SUBCATEGORY,'Mmoney')-ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS',SUBCATEGORY,'Mmoney')
)
;

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'PROFIT','ENERGY_SPECIFIC_PROFIT','Money_per_MWh')$(IAGK_HASORPOT(Y,IA,G) AND SUM(COMMODITY,PRO_YCRAGF(Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,'TWh') > 0)) =
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'PROFIT','TOTAL_PROFIT','Mmoney')/SUM(COMMODITY,PRO_YCRAGF(Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,'TWh'))
;
* ------------- END OF PROFIT --------------
* --------- END OF ECONOMIC OUTPUT -----------

* ---------------------- ENVIRONMENT ----------------------

* ------------- CO2 EMISSIONS -------------

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh'))=
(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')
$ifi %CCS%==yes   -(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')*CCS_CO2CAPTEFF_G(G))$CCS_G(G)
)*IM_CO2(G)*IOF3P6
;

* ------------- END OF CO2 EMISSIONS -------------

* ---------------------- END OF ENVIRONMENT ----------------------




execute_unload "../output/MainResults_%CASEID%.gdx"
ECO_G_YCRAG,ECO_X_YCR,OBJ_YCR,EMI_YCRAG,
EL_PRICE_YCR,EL_PRICE_YCRST,DH_PRICE_YCRA,DH_PRICE_YCRAST,
PRO_YCRAGFST,EL_DEMAND_YCRST,DH_DEMAND_YCRAST,    PRO_YCRAFST
PRO_YCRAGF,EL_DEMAND_YCR,DH_DEMAND_YCRA,
CURT_YCRAGFST,CURT_YCRAGF,
G_CAP_YCRAF,X_CAP_YCR,
X_FLOW_YCRST,X_FLOW_YCR,
F_CONS_YCRAST,F_CONS_YCRA,
WEEKLY_HYDRO_STORAGE_VOLUME
;
