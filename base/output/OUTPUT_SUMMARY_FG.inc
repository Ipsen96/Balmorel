*Create output file for FutureGas
* adaptions to the original by tilseb@dtu.dk; Aug. 2019

SET CATEGORY /COSTS,INCOME/;
SET SUBCATEGORY /ELECTRICITY_SALE,GENERATION_CAPITAL_COSTS, GENERATION_FIXED_COSTS, GENERATION_OPERATIONAL_COSTS, GENERATION_FUEL_COSTS,TRANSMISSION_CAPITAL_COSTS, TRANSMISSION_OPERATIONAL_COSTS,GENERATION_CO2_COSTS,GENERATION_TAXES,GENERATION_SUBSIDIES,GENERATION_GRID_TARIFFS,TAXES,SUBSIDIES/;
SET COMMODITY /ELECTRICITY,DISTRICT_HEAT,PROCESS_HEAT,SPACE_HEAT,IND_HEAT/;
SET TECH_TYPE /CONDENSING,CHP-BACK-PRESSURE,CHP-EXTRACTION,BOILERS,ELECT-TO-HEAT,INTERSEASONAL-HEAT-STORAGE,INTERSEASONAL-ELECT-STORAGE,INTRASEASONAL-HEAT-STORAGE,INTRASEASONAL-ELECT-STORAGE,HYDRO-RESERVOIRS,HYDRO-RUN-OF-RIVER,WIND-TURBINES,SOLAR-PV,SOLAR-HEATING,HYDRO-WAVE/;
SET GTECH_TYPE(GGG,TECH_TYPE);
SET PRICE_CATEGORY /AVERAGE,AVERAGE_WEIGHTED_BY_CONSUMPTION,AVERAGE_WEIGHTED_BY_PRODUCTION/;
SET VARIABLE_CATEGORY /EXOGENOUS,ENDOGENOUS,DECOMMISSIONING/;
SET UNITS /GW,TWh,MWh,Money_per_MWh,Mmoney,kton/;
SET EMISSION_TYPE /CO2,CH4/;
SET EUSER /OTHR,EV,P2X,HPandES_OTHER,HP_INDVH,HP_IND/;
*"OTHR" EUSER represents the exogenous electricity demand of the following groups: RESE,PII and OTHER

* sets to allow for sector distinguishability
SET INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE);
SET SECTOR /INDUSTRY,OTHER/;
SET INDUSTRY_AREAS(AAA) /DK1_Large_PHH,DK1_Large_PHL,DK1_NoDH_PHH,DK1_NoDH_PHL,DK2_Large_PHH,DK2_Large_PHL,DK2_NoDH_PHH,DK2_NoDH_PHL/;
*SET INDHEAT_AREAS(AAA)  /DK2_NoDH_ind,DK2_NoDH_apart,DK1_NoDH_ind,DK1_NoDH_apart/;


   GTECH_TYPE(G,'CONDENSING')                     = YES$((GDATA(G,'GDTYPE') EQ 1)  OR (GDATA(G,'GDTYPE') EQ GCND));
   GTECH_TYPE(G,'CHP-BACK-PRESSURE')              = YES$((GDATA(G,'GDTYPE') EQ 2)  OR (GDATA(G,'GDTYPE') EQ GBPR));
   GTECH_TYPE(G,'CHP-EXTRACTION')                 = YES$((GDATA(G,'GDTYPE') EQ 3)  OR (GDATA(G,'GDTYPE') EQ GEXT));
   GTECH_TYPE(G,'BOILERS')                        = YES$((GDATA(G,'GDTYPE') EQ 4)  OR (GDATA(G,'GDTYPE') EQ GHOB));
   GTECH_TYPE(G,'ELECT-TO-HEAT')                  = YES$((GDATA(G,'GDTYPE') EQ 5)  OR (GDATA(G,'GDTYPE') EQ GETOH));
   GTECH_TYPE(G,'INTRASEASONAL-HEAT-STORAGE')     = YES$((GDATA(G,'GDTYPE') EQ 6)  OR (GDATA(G,'GDTYPE') EQ GHSTO));
   GTECH_TYPE(G,'INTERSEASONAL-HEAT-STORAGE')     = YES$(GDATA(G,'GDTYPE') EQ GHSTOS);
   GTECH_TYPE(G,'INTRASEASONAL-ELECT-STORAGE')    = YES$((GDATA(G,'GDTYPE') EQ 7)  OR (GDATA(G,'GDTYPE') EQ GESTO));
   GTECH_TYPE(G,'INTERSEASONAL-ELECT-STORAGE')    = YES$(GDATA(G,'GDTYPE') EQ GESTOS);
   GTECH_TYPE(G,'HYDRO-RESERVOIRS')               = YES$((GDATA(G,'GDTYPE') EQ 8)  OR (GDATA(G,'GDTYPE') EQ GHYRS));
   GTECH_TYPE(G,'HYDRO-RUN-OF-RIVER')             = YES$((GDATA(G,'GDTYPE') EQ 9)  OR (GDATA(G,'GDTYPE') EQ GHYRR));
   GTECH_TYPE(G,'WIND-TURBINES')                  = YES$((GDATA(G,'GDTYPE') EQ 10) OR (GDATA(G,'GDTYPE') EQ GWND));
   GTECH_TYPE(G,'SOLAR-PV')                       = YES$((GDATA(G,'GDTYPE') EQ 11) OR (GDATA(G,'GDTYPE') EQ GSOLE));
   GTECH_TYPE(G,'SOLAR-HEATING')                  = YES$((GDATA(G,'GDTYPE') EQ 12) OR (GDATA(G,'GDTYPE') EQ GSOLH));
   GTECH_TYPE(G,'HYDRO-WAVE')                     = YES$((GDATA(G,'GDTYPE') EQ 22) OR (GDATA(G,'GDTYPE') EQ GWAVE));


* individual heating generation technology types
   INDHEAT_GTECH_TYPE(INDHEAT_G,'CONDENSING')                    = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GCND);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'CHP-BACK-PRESSURE')             = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GBPR);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'CHP-EXTRACTION')                = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GEXT);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'BOILERS')                       = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GHOB);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'ELECT-TO-HEAT')                 = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GETOH);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'INTRASEASONAL-HEAT-STORAGE')    = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GHSTO);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'INTERSEASONAL-HEAT-STORAGE')    = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GHSTOS);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'INTRASEASONAL-ELECT-STORAGE')   = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GESTO);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'INTERSEASONAL-ELECT-STORAGE')   = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GESTOS);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'HYDRO-RESERVOIRS')              = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GHYRS);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'HYDRO-RUN-OF-RIVER')            = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GHYRR);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'WIND-TURBINES')                 = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GWND);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'SOLAR-PV')                      = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GSOLE);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'SOLAR-HEATING')                 = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GSOLH);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'HYDRO-WAVE')                    = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GWAVE);


PARAMETER G_CAP_YCRAF(Y,C,RRR,*,*,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS) "Generation capacity for each year, country, region, area, technology, fuel, commodity, technology type, variable type (GW)";
PARAMETER PRO_YCRAGFST(Y,C,RRR,*,*,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Energy Production for each year, country, region, area, technology, fuel, season, hour, commodity, technology type (MWh)" ;
PARAMETER PRO_YCRAGF(Y,C,RRR,*,*,FFF,COMMODITY,TECH_TYPE,UNITS) "Annual Energy Production for each year, country, region, area, technology, fuel, commodity, technology type (TWh)";
PARAMETER F_CONS_YCRAST(Y,C,RRR,*,*,FFF,SSS,TTT,TECH_TYPE,UNITS,SECTOR) "Fuel consumption for each year, country, region, area, technology, fuel, season, hour and technology type (MWh)";
PARAMETER F_CONS_YCRA(Y,C,RRR,*,*,FFF,TECH_TYPE,UNITS,SECTOR) "Fuel consumption for each year, country, region, area, technology, fuel and technology type (TWh)";
PARAMETER X_CAP_YCR(Y,C,IRRRE,IRRRI,VARIABLE_CATEGORY,UNITS) "Transmission capacity for each year, country, from region to region (GW)";
PARAMETER X_FLOW_YCRST(Y,C,IRRRE,IRRRI,SSS,TTT,UNITS) "Transmission flow for each year, country, from region to region, for each season and hour (MWh)";
PARAMETER X_FLOW_YCR(Y,C,IRRRE,IRRRI,UNITS) "Transmission flow for each year, country, from region to region (TWh)";
PARAMETER ECO_G_YCRAG(Y,C,RRR,AAA,G,CATEGORY,SUBCATEGORY,UNITS) "Generation Economic Output for each year, country, region, area and technology (Mmoney)";
PARAMETER ECO_X_OUTPUT_YCR(Y,C,RRR,CATEGORY,SUBCATEGORY,UNITS) "Transmission Economic Output for each year, country and region (Mmoney)";
PARAMETER OBJ_YCR(Y,C,RRR,SUBCATEGORY,UNITS) "Objective function (Mmoney)";
PARAMETER EL_PRICE_YCR(Y,C,RRR,PRICE_CATEGORY,UNITS) "Average Electricity Prices for each region (money/MWh)";
PARAMETER EL_PRICE_YCRST(Y,C,RRR,SSS,TTT,UNITS) "Hourly Electricity Prices for each region (money/MWh)";
PARAMETER DH_PRICE_YCRA(Y,C,RRR,AAA,PRICE_CATEGORY,UNITS) "Average District Heating Prices for each area(money/MWh)";
PARAMETER DH_PRICE_YCRAST(Y,C,RRR,AAA,SSS,TTT,UNITS) "Hourly District Heating Prices for each area (money(MWh)";
*PARAMETER EL_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,UNITS,*) "Aggregated Hourly Electricity Demand (MWh)";
PARAMETER EL_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,EUSER,UNITS) "Aggregated Hourly Electricity Demand (MWh)";
PARAMETER DH_DEMAND_YCRAST(Y,C,RRR,AAA,SSS,TTT,COMMODITY,VARIABLE_CATEGORY,UNITS,*) "Hourly DH Demand (MWh)";
PARAMETER EMI_YCRAG(Y,C,RRR,*,*,FFF,TECH_TYPE,UNITS,SECTOR,EMISSION_TYPE) "Annual CO2-EQUIVALENT emissions (ktons)";
*PARAMETER CH(Y,C,RRR,*,*,FFF,TECH_TYPE,UNITS) "CH4 emissions"

* individual heating parameters
PARAMETER INDHEAT_F_CONS_YCRAST(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,SSS,TTT,TECH_TYPE,UNITS,INDHEAT_GROUP) "Fuel consumption for each year, country, region, area, technology, fuel, season, hour and technology type (MWh)";
PARAMETER INDHEAT_F_CONS_YCRA(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,UNITS,INDHEAT_GROUP) "Fuel consumption for each year, country, region, area, technology, fuel and technology type (TWh)";
PARAMETER INDHEAT_EMI_YCRAG(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,UNITS,INDHEAT_GROUP,EMISSION_TYPE) "Annual CO2 emissions(ktons) from individual heating (NOT INCLUDING EMISSIONS FROM ELECTRICITY)";


*Modification of IHOURSINST
IWEIGHSUMS = SUM(S, WEIGHT_S(S));
IWEIGHSUMT = SUM(T, WEIGHT_T(T));
IHOURSINST(SSS,TTT)=0;
IHOURSINST(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*IWEIGHSUMT);


* -------------------- ENERGY CAPACITY ---------------------

* ------------- ELECTRICITY CAPACITY --------------

** EXOGENOUS ELECTRICITY CAPACITY
G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
         IOF0001*(GKFX(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE))));

** ENDOGENOUS ELECTRICITY CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
         IOF0001*(VGKNACCUMNET.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE))));

** ENDOGENOUS ELECTRICITY CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) AND NOT INDUSTRY_AREAS(IA))=
$ifi %DECOM%==yes -IOF0001*(VDECOM_EXO_ACCUM.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE))));

* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT CAPACITY --------------

** EXOGENOUS HEAT CAPACITY
*** DISTRICT HEAT  (INCL. SPACE HEAT)
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
         IOF0001*(GKFX(Y,IA,IGH)/(1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH))));

*** INDHEAT
G_CAP_YCRAF(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,'IND_HEAT',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
         sum(INDHEAT_GROUP,IOF0001*(INDHEAT_GKFX(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP)));

*** INDUSTRY
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'PROCESS_HEAT',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND INDUSTRY_AREAS(IA))=
         IOF0001*(GKFX(Y,IA,IGH)/(1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH))));


** ENDOGENOUS HEAT CAPACITY ACCUMULATED INVESTMENTS
*** DISTRICT HEAT
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
         IOF0001*(VGKNACCUMNET.L(Y,IA,IGH)/(1$((NOT IGESTO(IGH)) AND (NOT IGESTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGESTO(IGH)) OR IGESTOS(IGH))));

*** INDHEAT
G_CAP_YCRAF(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,'IND_HEAT',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
         SUM(INDHEAT_GROUP,IOF0001*(INDHEAT_VGKNACCUMNET.L(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP)));

*** INDUSTRY
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'PROCESS_HEAT',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND INDUSTRY_AREAS(IA))=
         IOF0001*(VGKNACCUMNET.L(Y,IA,IGH)/(1$((NOT IGESTO(IGH)) AND (NOT IGESTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGESTO(IGH)) OR IGESTOS(IGH))));

** ENDOGENOUS HEAT CAPACITY ACCUMULATED DECOMMISSIONING
*** DISTRICT HEAT
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND NOT INDUSTRY_AREAS(IA))=
$ifi %DECOM%==yes -IOF0001*(VDECOM_EXO_ACCUM.L(Y,IA,IGH)/(1$((NOT IGESTO(IGH)) AND (NOT IGESTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGESTO(IGH)) OR IGESTOS(IGH))));

*** INDUSTRY
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'PROCESS_HEAT',TECH_TYPE,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND INDUSTRY_AREAS(IA))=
$ifi %DECOM%==yes -IOF0001*(VDECOM_EXO_ACCUM.L(Y,IA,IGH)/(1$((NOT IGESTO(IGH)) AND (NOT IGESTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGESTO(IGH)) OR IGESTOS(IGH))));



* ------------- END OF HEAT CAPACITY --------------

* ---------------- END OF ENERGY CAPACITY -----------------


* -------------------- ENERGY PRODUCTION ---------------------

* ------------------------ ELECTRICITY PRODUCTION ---------------------
** ELECTRICITY PRODUCTION HOURLY
*** OTHER SECTOR
PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
       VGE_T.L(Y,IA,IGE,S,T);

** ELECTRICITY PRODUCTION ANNUALLY
*** OTHER SECTOR
PRO_YCRAGF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'TWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
       SUM((S,T),IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T))/IOF1000000;

* ------------------------ END OF ELECTRICITY PRODUCTION --------------

* ------------------------ HEAT PRODUCTION ---------------------
** HEAT PRODUCTION HOURLY
*** DISTRICT HEAT
PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'DISTRICT_HEAT',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
       VGH_T.L(Y,IA,IGH,S,T);

*** INDHEAT
PRO_YCRAGFST(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,S,T,'IND_HEAT',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
      sum((INDHEAT_DHUSER,INDHEAT_GROUP),INDHEAT_VGH_T.L(Y,INDHEAT_AAA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T));

*** PROCESS HEAT
PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'PROCESS_HEAT',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND INDUSTRY_AREAS(IA))=
       VGH_T.L(Y,IA,IGH,S,T);


** HEAT PRODUCTION ANNUAL
*** DISTRICT HEAT
PRO_YCRAGF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
       SUM((S,T),IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T))/IOF1000000;

*** INDHEAT
PRO_YCRAGF(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,'IND_HEAT',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
      SUM((S,T,INDHEAT_DHUSER,INDHEAT_GROUP),IHOURSINST(S,T)*INDHEAT_VGH_T.L(Y,INDHEAT_AAA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T))/IOF1000000;

*** PROCESS HEAT
PRO_YCRAGF(Y,C,IR,IA,IGH,FFF,'PROCESS_HEAT',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND INDUSTRY_AREAS(IA))=
       SUM((S,T),IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T))/IOF1000000;


* ------------------------ END OF HEAT PRODUCTION --------------

* ---------------- END OF ENERGY PRODUCTION -----------------


* -------------------- FUEL CONSUMPTION ---------------------
** HOURLY FUEL CONSUMPTION

*** OTHER SECTOR (INCL. INDHEAT)
F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,'MWh','OTHER')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
       VGF_T.L(Y,IA,G,S,T);
F_CONS_YCRAST(Y,C,IR,IA,'GNR_EH_HEAT',FFF,S,T,TECH_TYPE,'MWh','OTHER')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA))=
       VGF_T.L(Y,IA,'GNR_EH_HEAT',S,T);

*** INDHEAT
INDHEAT_F_CONS_YCRAST(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,S,T,TECH_TYPE,'MWh',INDHEAT_GROUP)$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
       INDHEAT_VGF_T.L(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T);

*F_CONS_YCRAST(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,S,T,TECH_TYPE,'MWh',INDHEAT_GROUP)$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
*       INDHEAT_VGF_T.L(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T);
F_CONS_YCRAST(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,S,T,TECH_TYPE,'MWh','OTHER')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
       SUM(INDHEAT_GROUP,INDHEAT_VGF_T.L(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T));

*** INDUSTRY SECTOR
F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,'MWh','INDUSTRY')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND INDUSTRY_AREAS(IA))=
       VGF_T.L(Y,IA,G,S,T);
F_CONS_YCRAST(Y,C,IR,IA,'GNR_EH_HEAT',FFF,S,T,TECH_TYPE,'MWh','INDUSTRY')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA))=
       0;


** ANNUAL FUEL CONSUMPTION
*** OTHER SECTOR (INCL. INDHEAT)
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))/IOF1000000;
F_CONS_YCRA(Y,C,IR,IA,'GNR_EH_HEAT',FFF,TECH_TYPE,'TWh','OTHER')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,'GNR_EH_HEAT',S,T))/IOF1000000;

*** INDHEAT
INDHEAT_F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh',INDHEAT_GROUP)$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*INDHEAT_VGF_T.L(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T))/IOF1000000;

*F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh',INDHEAT_GROUP)$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
*       SUM((S,T),IHOURSINST(S,T)*INDHEAT_VGF_T.L(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T))/IOF1000000;
F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_AAA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
      SUM((S,T,INDHEAT_GROUP),IHOURSINST(S,T)*INDHEAT_VGF_T.L(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T))/IOF1000000;

*** INDUSTRY SECTOR
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND INDUSTRY_AREAS(IA))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))/IOF1000000;
F_CONS_YCRA(Y,C,IR,IA,'GNR_EH_HEAT',FFF,TECH_TYPE,'TWh','INDUSTRY')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA))=
       0;

**** FUEL CONSUMPTION EQUATIONS FROM STANDARD OUTPUT FILE
$ONTEXT
** HOURLY FUEL CONSUMPTION
F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       VGF_T.L(Y,IA,G,S,T);

** ANNUAL FUEL CONSUMPTION
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))/IOF1000000;
$OFFTEXT


* ---------------- END OF FUEL CONSUMPTION -----------------


* ------------------------- ENERGY DEMAND ----------------------

* ------------- ELECTRICITY DEMAND --------------

** EXOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','OTHR','MWh')$CCCRRR(C,IR)=
         SUM(DEUSER,IDE_T(Y,IR,'PII',S,T))/(1-DISLOSS_E(IR))  ;
EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','OTHR','MWh')$CCCRRR(C,IR) =
         SUM(DEUSER,IDE_T(Y,IR,'RESE',S,T))/(1-DISLOSS_E(IR))  ;
EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','OTHR','MWh')$CCCRRR(C,IR) =
         SUM(DEUSER,IDE_T(Y,IR,'OTHER',S,T))/(1-DISLOSS_E(IR));
EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','EV','MWh')$CCCRRR(C,IR) =
         SUM(DEUSER,IDE_T(Y,IR,'EV',S,T))/(1-DISLOSS_E(IR)) ;
EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','P2X','MWh')$CCCRRR(C,IR) =
         SUM(DEUSER,IDE_T(Y,IR,'PTX',S,T))/(1-DISLOSS_E(IR)) ;

** ENDOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','HPandES_OTHER','MWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
           SUM(IA$(RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA)), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)))
* PREVIOUS SUM(IA$(RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA)), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)))

* INTRASEASONAL ELECTRICITY STORAGE
         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADT.L(Y,IA,IGESTO,S,T)))
* INTERSEASONAL ELECTRICITY STORAGE
         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),VESTOLOADTS.L(Y,IA,IGESTOS,S,T)));

*** IND_HEAT SECTOR
EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','HP_INDVH','MWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
          SUM(INDHEAT_AAA$RRRAAA(IR,INDHEAT_AAA), SUM((INDHEAT_IGETOH,INDHEAT_GROUP)$INDHEAT_IAGK_HASORPOT(Y,INDHEAT_AAA,INDHEAT_IGETOH,INDHEAT_GROUP), INDHEAT_VGF_T.L(Y,INDHEAT_AAA,INDHEAT_IGETOH,INDHEAT_GROUP,S,T))) ;

*** INDUSTRY SECTOR
EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','HP_IND','MWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
        SUM(IA$(RRRAAA(IR,IA) AND INDUSTRY_AREAS(IA)), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)));

************ PREVIOUS********************************
*** OTHER SECTOR (INCL. INDHEAT)
*** IND_HEAT SECTOR
**********(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,S,T,TECH_TYPE,'MWh','OTHER')
*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','MWh','OTHER')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
*         SUM(INDHEAT_AAA$RRRAAA(IR,INDHEAT_AAA), SUM(IGETOH, VGE_T.L(Y,INDHEAT_AAA,IGETOH,S,T)))

* INTRASEASONAL ELECTRICITY STORAGE
*          +SUM(INDHEAT_AAA$RRRAAA(IR,INDHEAT_AAA),SUM(IGESTO,VESTOLOADT.L(Y,INDHEAT_AAA,IGESTO,S,T)))

* INTERSEASONAL ELECTRICITY STORAGE
*         +SUM(INDHEAT_AAA$RRRAAA(IR,INDHEAT_AAA),SUM(IGESTOS,VESTOLOADTS.L(Y,INDHEAT_AAA,IGESTOS,S,T)));


*** INDUSTRY SECTOR
*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','MWh','INDUSTRY')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
*        SUM(IA$(RRRAAA(IR,IA) AND INDUSTRY_AREAS(IA)), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)))
* INTRASEASONAL ELECTRICITY STORAGE
*         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADT.L(Y,IA,IGESTO,S,T)))
* INTERSEASONAL ELECTRICITY STORAGE
*         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),VESTOLOADTS.L(Y,IA,IGESTOS,S,T)));
************ PREVIOUS********************************



* ----------- END OF ELECTRICITY DEMAND --------------

* ------------- HEAT DEMAND --------------

** EXOGENOUS DEMAND
*** OTHER SECTOR (INCL. INDHEAT)
DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,'DISTRICT_HEAT','EXOGENOUS','MWh','OTHER')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA))=
         IDH_T(Y,IA,'RESH',S,T)/(1-DISLOSS_H(IA));

DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,'DISTRICT_HEAT','EXOGENOUS','MWh','INDUSTRY')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA))=
         IDH_T(Y,IA,'SH',S,T)/(1-DISLOSS_H(IA));

*** INDHEAT SECTOR
DH_DEMAND_YCRAST(Y,C,IR,INDHEAT_AAA,S,T,'IND_HEAT','EXOGENOUS','MWh','OTHER')$(CCCRRR(C,IR) AND RRRAAA(IR,INDHEAT_AAA))=
         SUM(INDHEAT_DHUSER,INDHEAT_DH_VAR_T(INDHEAT_AAA,INDHEAT_DHUSER,S,T));

*** INDUSTRY SECTOR
DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,'PROCESS_HEAT','EXOGENOUS','MWh','INDUSTRY')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND INDUSTRY_AREAS(IA))=
         SUM(DHUSER,IDH_T(Y,IA,DHUSER,S,T))/(1-DISLOSS_H(IA));

** ENDOGENOUS DEMAND
*** OTHER SECTOR
DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,'DISTRICT_HEAT','ENDOGENOUS','MWh','OTHER')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA))=
* INTRASEASONAL HEAT STORAGE
        SUM(IGHSTO$IAGK_HASORPOT(Y,IA,IGHSTO),VHSTOLOADT.L(Y,IA,IGHSTO,S,T))
* INTERSEASONAL HEAT STORAGE
        + SUM(IGHSTOS$IAGK_HASORPOT(Y,IA,IGHSTOS),VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T));

* ----------- END OF HEAT DEMAND --------------

* -------------------- END OF ENERGY DEMAND -----------------------



* -------------------- TRANSMISSION CAPACITY -----------------------

** EXOGENOUS ELECTRICITY CAPACITY

X_CAP_YCR(Y,C,IR,IRI,'EXOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*XKFX(Y,IR,IRI)
;


** ENDOGENOUS ELECTRIC CAPACITY

$ifi not %TransInvest%==yes $goto Transmission_investments_end
X_CAP_YCR(Y,C,IR,IRI,'ENDOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*VXKNACCUMNET.L(Y,IR,IRI)
;
$label  Transmission_investments_end


* -------------------- END OF TRANSMISSION CAPACITY -----------------------

* -------------------- TRANSMISSION FLOW -----------------------
** HOURLY TRANSMISSION FLOW
X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
VX_T.L(Y,IR,IRI,S,T);
;

** ANNUAL TRANSMISSION FLOW
X_FLOW_YCR(Y,C,IR,IRI,'TWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
SUM((S,T),IHOURSINST(S,T)*VX_T.L(Y,IR,IRI,S,T))/IOF1000000;
;

* -------------------- END OF TRANSMISSION FLOW -----------------------



* ---------------------- ECONOMIC OUTPUT ----------------------

* ------------- COSTS --------------

* -- GENERATION COSTS -----

** GENERATION INVESTMENTS

ECO_G_YCRAG(Y,C,IR,IA,G,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and  VGKN.L(Y,IA,G))=
         OMONEY*
         (VGKN.L(Y,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G)*SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),IYHASANNUITYG(Y,IYALIAS2,G))
*investments in previous years
         + SUM(IYALIAS2$(ORD(IYALIAS2) LT ORD(Y)), VGKN.L(IYALIAS2,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G)*IYHASANNUITYG(IYALIAS2,Y,G))
         );

** GENEARTION FIXED COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,'COSTS','GENERATION_FIXED_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000*(GKFX(Y,IA,G)+VGKNACCUMNET.L(Y,IA,G))*GOMFCOST(IA,G);

** GENERATION O&M COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY/IOF1000000*
     ((GOMVCOSTO(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGE_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKE(G)

   +  (GOMVCOSTO(IA,G) * GDATA(G,'GDCV') *
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGH_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKH(G)

   +  (GOMVCOSTIN(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))
   +   SUM((S,T), IHOURSINST(S,T) * HYPPROFILS(IA,S) *VGE_T.L(Y,IA,G,S,T))$IGHYRS(G)
     ) ;

** GENERATION FUEL COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,'COSTS','GENERATION_FUEL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY*IOF3P6/IOF1000000*
    SUM(FFF$(GDATA(G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')),
                 FUELPRICE(Y,IA,FFF)*(
                 SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T) )$IAGK_HASORPOT(Y,IA,G)
                )
    )$(IGNOTETOH(G) and (not IGESTO(G)) and (not IGHSTO(G)) and (not IGESTOS(G)) and (not IGHSTOS(G)));

** CO2 EMISSIONS COST
ECO_G_YCRAG(Y,C,IR,IA,G,'COSTS','GENERATION_CO2_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000000*IM_CO2(G)*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * SUM((S,T), IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G);

**TAXES
$ifi NOT %TAXES%==yes  $goto No_TAXES
ECO_G_YCRAG(Y,C,IR,IA,G,'COSTS','GENERATION_TAXES','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
OMONEY/IOF1000000*(
* Tax(+)/subsidy(-) on fuel use for electricity generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_F_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))
* Tax(+)/subsidy(-) on fuel use for heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_F_HEAT(Y,C,G) * GDATA(G,'GDCV') * VGH_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))
* Tax(+)/subsidy(-) on fuel use
       + SUM((IS3,T),
              IHOURSINST(IS3,T) * TAX_F(Y,C,G)  * VGF_T.L(Y,IA,G,IS3,T))
* Tax(+)/subsidy(-) on heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_G_HEAT(Y,C,G)  * VGH_T.L(Y,IA,G,IS3,T))
* Tax(+)/subsidy(-) on electricity generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_G_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T))
)
;
$label NO_TAXES

*GRID TARIFFS
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS
ECO_G_YCRAG(Y,C,IR,IA,IGETOH,'COSTS','GENERATION_GRID_TARIFFS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGETOH))=
OMONEY/IOF1000000*(
* Fixed component and subscribed power (per MW installed)
                 + IOF1000 *
                         IGR_PRICE_TECH(Y,IR,'GRDSUBS')  * (VGKNACCUMNET.L(Y,IA,IGETOH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGETOH)$IGDECOMEXOPOT(Y,IA,IGETOH)
                          + GKFX(Y,IA,IGETOH)) /(GDATA(IGETOH,'GDFE') *(1$(NOT GEFFRATE(IA,IGETOH))+GEFFRATE(IA,IGETOH)))
* Energy charge winter
                 + SUM((IS3,T)$IGR_TIME('GRDECW',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDECW')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge summer
                 + SUM((IS3,T)$IGR_TIME('GRDECS',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDECS')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 1
                 + SUM((IS3,T)$IGR_TIME('GRDTOU1',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU1')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 2
                 + SUM((IS3,T)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU2')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 3
                 + SUM((IS3,T)$IGR_TIME('GRDTOU3',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU3')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 4
                 + SUM((IS3,T)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU4')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Demand charge winter
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VGETOH_CAP1.L(Y,IA,MMM) *  IGR_PRICE_TECH(Y,IR,'GRDDCW'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge winter
* Demand charge summer
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VGETOH_CAP1.L(Y,IA,MMM) *  IGR_PRICE_TECH(Y,IR,'GRDDCS'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge summer
* Demand charge 2 (e.g for spring and fall months)
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VGETOH_CAP1.L(Y,IA,MMM) *  IGR_PRICE_TECH(Y,IR,'GRDDC2'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge 2
* Demand charge TOU
                 + IOF1000 * SUM((MMM),   VGETOH_CAP2.L(Y,IA,MMM) *  IGR_PRICE_TECH(Y,IR,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
* Demand charge yearly
                 + IOF1000 * VGETOH_CAPY.L(Y,IA) *  IGR_PRICE_TECH(Y,IR,'GRDDCY')
)
;
$label NO_GRIDTARIFFS

* -- TRANSMISSION COSTS -----

** TRANSMISSION INVESTMENTS COSTS
ECO_X_OUTPUT_YCR(Y,C,IR,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
          OMONEY/IOF1000000*(
             SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),IYHASANNUITYX(IYALIAS2,Y))*(
*EXPORTING REGION
                 SUM(IRI$IXK_HASORPOT(Y,IR,IRI), IOF05*VXKN.L(Y,IR,IRI)*XINVCOST(Y,IR,IRI)*IOF05*ANNUITYCX(C))
*IMPORTING REGION
                 + SUM(IRE$IXK_HASORPOT(Y,IRE,IR), IOF05*VXKN.L(Y,IRE,IR)*XINVCOST(Y,IRE,IR)*IOF05*ANNUITYCX(C))
             )
*investments in previous years
             + SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),IYHASANNUITYX(IYALIAS2,Y)*(
*EXPORTING REGION
                 SUM(IRI$IXK_HASORPOT(IYALIAS2,IR,IRI),IOF05*VXKN.L(IYALIAS2,IR,IRI)*XINVCOST(IYALIAS2,IR,IRI)*IOF05*ANNUITYCX(C))
*IMPORTING REGION
                 + SUM(IRE$IXK_HASORPOT(IYALIAS2,IRE,IR), IOF05*VXKN.L(IYALIAS2,IRE,IR)*XINVCOST(IYALIAS2,IRE,IR)*IOF05*ANNUITYCX(C))
            ))
                                );

** TRANSMISSION FLOW COSTS

ECO_X_OUTPUT_YCR(Y,C,IR,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IR,IRI), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * XCOST(IR,IRI))));

* ------------- END OF COSTS --------------

* ------------- INCOME --------------

$ifi not %SUBSIDIES%==yes $goto endsubsidies
** SUBSIDIES
ECO_G_YCRAG(Y,C,IR,IA,G,'INCOME','GENERATION_SUBSIDIES','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
OMONEY/IOF1000000*(
* Subsidy on fuel use for electricity generation
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND IGE(G) AND SUBSIDIES_F_EL(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_F_EL(Y,C,G)  * VGE_T.l(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * GEFFRATE(IA,G))))
* Subsidy on fuel use for heat generation
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND IGH(G) AND SUBSIDIES_F_HEAT(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_F_HEAT(Y,C,G) * GDATA(G,'GDCV') * VGH_T.l(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * GEFFRATE(IA,G))))
* Subsidy on fuel use
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND SUBSIDIES_F(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_F(Y,C,G)  * VGF_T.l(Y,IA,G,IS3,T)))
* Subsidy on heat generation
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND SUBSIDIES_G_HEAT(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_G_HEAT(Y,C,G)  * VGH_T.l(Y,IA,G,IS3,T)))
* Subsidy on electricity generation
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND SUBSIDIES_G_EL(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_G_EL(Y,C,G)  * VGE_T.l(Y,IA,G,IS3,T)))
* Subsidy on electricity consumption
    - SUM((DEUSER), SUBSIDIES_DE(C,DEUSER) * DE(Y,IR,DEUSER))
* Subsidy on heat consumption
    - SUM((DHUSER), SUBSIDIES_DH(C,DHUSER) * DH(Y,IA,DHUSER))
);
$label endsubsidies

* ------------- END OF INCOME --------------

* ------------- OBJECTIVE FUNCTION --------------

OBJ_YCR(Y,C,IR,SUBCATEGORY,'Mmoney')$CCCRRR(C,IR)=

*GENERATION COSTS
SUM((IA,G)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,'COSTS',SUBCATEGORY,'Mmoney'))

*TRANSMISSION COSTS
+ ECO_X_OUTPUT_YCR(Y,C,IR,'COSTS',SUBCATEGORY,'Mmoney')

*INCOME (only subsidies)
+ SUM((IA,G)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,'INCOME',SUBCATEGORY,'Mmoney'))
;

* ------------- END OF OBJECTIVE FUNCTION --------------

* ------------- INCOME --------------

** ELECTRICITY SALES
ECO_G_YCRAG(Y,C,IR,IA,G,'INCOME','ELECTRICITY_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) and IGE(G))=
 - OMONEY*IOF0000001*
   (SUM((S,T), VGE_T.L(Y,IA,G,S,T)*QEEQ.M(Y,IR,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)))*(1-2$(IGETOH(G)));

* ------------- END OF INCOME --------------

* ------------- COMMODITY PRICES --------------

** ELECTRICITY PRICES

*HOURLY ELECTRICITY PRICE

EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')$CCCRRR(C,IR)=OMONEY*QEEQ.M(Y,IR,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)/IHOURSINST(S,T);

*AVERAGE ELECTRICITY PRICE

EL_PRICE_YCR(Y,C,IR,'AVERAGE','Money_per_MWh')$CCCRRR(C,IR)=
OMONEY/IOF8760/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)*SUM((S,T),QEEQ.M(Y,IR,S,T));

** END OF ELECTRICITY PRICES

** DISTRICT HEATING PRICES

*HOURLY DISTRICT HEATING PRICE

DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=OMONEY*QHEQ.M(Y,IA,S,T)/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)/IHOURSINST(S,T);

*AVERAGE DISTRICT HEATING PRICE

DH_PRICE_YCRA(Y,C,IR,IA,'AVERAGE','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
OMONEY/IOF8760/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y)*SUM((S,T),QHEQ.M(Y,IA,S,T));

$ontext
*EXCLUDED SO FAR
*AVERAGE DISTRICT HEATING PRICE WEIGTHED BY CONSUMPTION

DH_PRICE_YCRA(Y,C,IR,IA,'AVERAGE_WEIGHTED_BY_CONSUMPTION','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
               SUM((VARIABLE_CATEGORY,S,T),
                         DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,VARIABLE_CATEGORY,'MWh')
                         )
               /SUM((VARIABLE_CATEGORY,S,T),DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,VARIABLE_CATEGORY,'MWh'))
;

*AVERAGE DISTRICT HEATING PRICE WEIGTHED BY PRODUCTION

DH_PRICE_YCRA(Y,C,IR,IA,'AVERAGE_WEIGHTED_BY_PRODUCTION','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
               SUM((IGH,FFF,S,T,TECH_TYPE)$IAGK_HASORPOT(Y,IA,IGH),
                         DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'DISTRICT_HEAT',TECH_TYPE,'MWh')
                         )
               /SUM((IGH,FFF,S,T,TECH_TYPE)$IAGK_HASORPOT(Y,IA,IGH),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'DISTRICT_HEAT',TECH_TYPE,'MWh'))
;
$offtext

** END OF DISTRICT HEATING PRICES

* ------------- END OF COMMODITY PRICES --------------

* --------- END OF ECONOMIC OUTPUT -----------


* ---------------------- ENVIRONMENT ----------------------

* ------------- CO2 EMISSIONS -------------
*----Sum of emissions
*$ontext
*IF YOU WANT TO SEE EMISSIONS BY OTHER AND INDUSTRY (ALL ELC EMISSIONS IS IN OTHER)
*NEED TO ADD 'SECTOR' TO THE PARAMETER DEFINE AREA
EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton','OTHER','CO2')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER') AND NOT INDUSTRY_AREAS(IA))=
        F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER')*IM_CO2(G)*IOF3P6;

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton','OTHER','CH4')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER') AND NOT INDUSTRY_AREAS(IA))=
        F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER')*GDATA(G,'GDCH4')*CO2EQUIVALENT_FOR_CH4*IOF0001*IOF3P6;



EMI_YCRAG(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'kton','OTHER','CO2')$(F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER') AND RRRAAA(IR,INDHEAT_AAA))=
        F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER')*FDATA(FFF,'FDCO2')*IOF3P6;

EMI_YCRAG(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'kton','OTHER','CH4')$(F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER') AND RRRAAA(IR,INDHEAT_AAA))=
        F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER')*INDHEAT_GDATA(INDHEAT_G,'GDCH4')*CO2EQUIVALENT_FOR_CH4*IOF0001*IOF3P6;



*--INDUSTRY SECTOR
EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton','INDUSTRY','CO2')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY') AND INDUSTRY_AREAS(IA))=
        F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY')*IM_CO2(G)*IOF3P6;

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton','INDUSTRY','CH4')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY') AND INDUSTRY_AREAS(IA))=
        F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY')*GDATA(G,'GDCH4')*CO2EQUIVALENT_FOR_CH4*IOF0001*IOF3P6;
*$offtext

$ONTEXT

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton','CO2')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER') AND NOT INDUSTRY_AREAS(IA))=
        F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER')*IM_CO2(G)*IOF3P6;

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton','CH4')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER') AND NOT INDUSTRY_AREAS(IA))=
        F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','OTHER')*GDATA(G,'GDCH4')*CO2EQUIVALENT_FOR_CH4*IOF0001*IOF3P6;




EMI_YCRAG(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'kton','CO2')$(F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER') AND RRRAAA(IR,INDHEAT_AAA))=
        F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER')*FDATA(FFF,'FDCO2')*IOF3P6;

EMI_YCRAG(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'kton','CH4')$(F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER') AND RRRAAA(IR,INDHEAT_AAA))=
        F_CONS_YCRA(Y,C,IR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh','OTHER')*INDHEAT_GDATA(INDHEAT_G,'GDCH4')*CO2EQUIVALENT_FOR_CH4*IOF0001*IOF3P6;




*--INDUSTRY SECTOR
EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton','CO2')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY') AND INDUSTRY_AREAS(IA))=
        F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY')*IM_CO2(G)*IOF3P6;

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton','CH4')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY') AND INDUSTRY_AREAS(IA))=
        F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh','INDUSTRY')*GDATA(G,'GDCH4')*CO2EQUIVALENT_FOR_CH4*IOF0001*IOF3P6;

$OFFTEXT

*----INDHEAT Technology Emissions
INDHEAT_EMI_YCRAG(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'kton',INDHEAT_GROUP,'CO2')$(INDHEAT_F_CONS_YCRA(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh',INDHEAT_GROUP))=
        INDHEAT_F_CONS_YCRA(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh',INDHEAT_GROUP)*FDATA(FFF,'FDCO2')*IOF3P6;

INDHEAT_EMI_YCRAG(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'kton',INDHEAT_GROUP,'CH4')$(INDHEAT_F_CONS_YCRA(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh',INDHEAT_GROUP))=
        INDHEAT_F_CONS_YCRA(Y,C,RRR,INDHEAT_AAA,INDHEAT_G,FFF,TECH_TYPE,'TWh',INDHEAT_GROUP)*INDHEAT_GDATA(INDHEAT_G,'GDCH4')*CO2EQUIVALENT_FOR_CH4*IOF0001*IOF3P6;


* ------------- END OF CO2 EMISSIONS -------------

* ---------------------- END OF ENVIRONMENT ----------------------




execute_unload "MainResults%SCNAME%%TECHALT%.gdx" ECO_G_YCRAG,ECO_X_OUTPUT_YCR,OBJ_YCR,EL_PRICE_YCR,EL_PRICE_YCRST,PRO_YCRAGFST,EL_DEMAND_YCRST,
DH_DEMAND_YCRAST,PRO_YCRAGF,DH_PRICE_YCRAST,DH_PRICE_YCRA,G_CAP_YCRAF,X_CAP_YCR,X_FLOW_YCRST,X_FLOW_YCR,F_CONS_YCRAST,F_CONS_YCRA,EMI_YCRAG,INDHEAT_F_CONS_YCRAST,INDHEAT_F_CONS_YCRA,INDHEAT_EMI_YCRAG
;


