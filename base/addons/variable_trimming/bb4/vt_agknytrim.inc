*************************************
*Defining the costs
**************************************

* Fixed costs including investment costs
VT_FIXED(Y,IA,G)$IAGKNY(Y,IA,G) = IOF1000*GOMFCOST(IA,G)  + IOF1000000*(GINVCOST(IA,G)*(SUM(C$ICA(C,IA),ANNUITYCG(C,G))));

* Variable costs for electricity generation
VT_VAREL(Y,IA,G)$(IAGKNY(Y,IA,G) AND  IGNOTETOH(G) AND IGE(G))= GOMVCOST(IA,G)
$ifi %TAXES%==yes         + SUM(C$ICA(C,IA), TAX_G_EL(Y,C,G))
    + (    SUM(FFF$IGF(G,FFF), FUELPRICE(Y,IA,FFF) * IOF3P6 )
         + IM_CO2(G)*IOF0001 * IOF3P6 * SUM(C$ICA(C,IA), M_POL(Y,"TAX_CO2",C))
         + IM_SO2(G)*IOF0001 * IOF3P6 * SUM(C$ICA(C,IA), M_POL(Y,"TAX_SO2",C))
         + GDATA(G,'GDNOX')*IOF0000001 * SUM(C$ICA(C,IA), M_POL(Y,"TAX_NOX",C))
$ifi %TAXES%==yes         + SUM(C$ICA(C,IA),TAX_F_EL(Y,C,G))
$ifi %TAXES%==yes         + SUM(C$ICA(C,IA),TAX_F(Y,C,G))
   )
          /(GDATA(G,'GDFE')*(1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))) ;

*Variable cost for heat generation
VT_VARHEAT(Y,IA,G)$(IAGKNY(Y,IA,G) AND IGH(G))  = (GOMVCOST(IA,G) * GDATA(G,'GDCV'))$IGKH(G)
$ifi %TAXES%==yes      + SUM(C$ICA(C,IA),TAX_G_HEAT(Y,C,G))
                 + GDATA(G,'GDCV')* (
                   SUM(FFF$IGF(G,FFF), FUELPRICE(Y,IA,FFF) * IOF3P6 )
                 + IM_CO2(G)*IOF0001 * IOF3P6 * SUM(C$ICA(C,IA), M_POL(Y,"TAX_CO2",C))
                 + IM_SO2(G)*IOF0001 * IOF3P6 * SUM(C$ICA(C,IA), M_POL(Y,"TAX_SO2",C))
                 + GDATA(G,'GDNOX')*IOF0000001 * SUM(C$ICA(C,IA), M_POL(Y,"TAX_NOX",C))
$ifi %TAXES%==yes                 + SUM(C$ICA(C,IA),TAX_F_HEAT(Y,C,G))
$ifi %TAXES%==yes                 + SUM(C$ICA(C,IA),TAX_F(Y,C,G))
    )
            /(GDATA(G,'GDFE')*(1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G)));

**************************************************************
*** technologies with fuel or technology potential constraints
****************************************************************

*IGMINF
VT_REQMIN(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), IGMINF(Y,C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMIN(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), IGMINF(Y,IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMIN(Y,IA,G)$(SUM(FFF$IGF(G,FFF), IGMINF(Y,IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);

*FKMIN
VT_REQMIN(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), FKMIN(Y,C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMIN(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), FKMIN(Y,IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMIN(Y,IA,G)$(SUM(FFF$IGF(G,FFF), FKMIN(Y,IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);

*IGEQF
VT_REQFX(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), IGEQF(Y,C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQFX(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), IGEQF(Y,IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQFX(Y,IA,G)$(SUM(FFF$IGF(G,FFF), IGEQF(Y,IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);

*FKEQ
VT_REQFX(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), FKEQ(Y,C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQFX(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), FKEQ(Y,IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQFX(Y,IA,G)$(SUM(FFF$IGF(G,FFF), FKEQ(Y,IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);

*IGMAXF
VT_REQMAX(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), IGMAXF(Y,C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMAX(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), IGMAXF(Y,IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMAX(Y,IA,G)$(SUM(FFF$IGF(G,FFF), IGMAXF(Y,IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);

*FKPOT
VT_REQMAX(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), FKPOT(C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMAX(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), FKPOT(IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMAX(Y,IA,G)$(SUM(FFF$IGF(G,FFF), FKPOT(IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);

*FKMAX
VT_REQMAX(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), FKMAX(Y,C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMAX(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), FKMAX(Y,IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMAX(Y,IA,G)$(SUM(FFF$IGF(G,FFF), FKMAX(Y,IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);


*TECHGROUPKPOT
VT_REQMAX(Y,IA,G)$(SUM((TECH_GROUP,C)$((GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP)) AND ICA(C,IA)), TECHGROUPKPOT(C,TECH_GROUP)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMAX(Y,IA,G)$(SUM((TECH_GROUP,IR)$((GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP)) AND RRRAAA(IR,IA)), TECHGROUPKPOT(IR,TECH_GROUP)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQMAX(Y,IA,G)$(SUM(TECH_GROUP$(GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP)), TECHGROUPKPOT(IA,TECH_GROUP)) >0) = YES$IAGKNY(Y,IA,G);

*FGEMIN
VT_REQEMIN(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), FGEMIN(C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQEMIN(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), FGEMIN(IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQEMIN(Y,IA,G)$(SUM(FFF$IGF(G,FFF), FGEMIN(IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);

*FGEMAX
VT_REQEMAX(Y,IA,G)$(SUM((FFF,C)$(IGF(G,FFF) AND ICA(C,IA)), FGEMAX(C,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQEMAX(Y,IA,G)$(SUM((FFF,IR)$(IGF(G,FFF) AND RRRAAA(IR,IA)), FGEMAX(IR,FFF)) >0) = YES$IAGKNY(Y,IA,G);
VT_REQEMAX(Y,IA,G)$(SUM(FFF$IGF(G,FFF), FGEMAX(IA,FFF)) >0) = YES$IAGKNY(Y,IA,G);



*************************************
*** Using the screening curves
**************************************

****** Condencing and heat only units  ******

*Cost per unit of electricity or heat generated for the cheapest plant in the screening curve (both with and without considering plants with maxium and fixed fuel requirements)
VT_CHEAPESTCND(Y,IA,VT_FLH) = SMIN(G$(IGCND(G)  AND IAGKNY(Y,IA,G) AND NOT VT_REQEMAX(Y,IA,G) AND NOT VT_REQMAX(Y,IA,G) AND NOT VT_REQFX(Y,IA,G)), VT_FIXED(Y,IA,G) + VT_VAREL(Y,IA,G) * VT_FLH.VAL);
VT_CHEAPESTHOB(Y,IA,VT_FLH) = SMIN(G$(IGHOB(G)  AND IAGKNY(Y,IA,G) AND NOT VT_REQEMAX(Y,IA,G) AND NOT VT_REQMAX(Y,IA,G) AND NOT VT_REQFX(Y,IA,G)), VT_FIXED(Y,IA,G) + VT_VARHEAT(Y,IA,G) * VT_FLH.VAL);
VT_CHEAPESTCNDF(Y,IA,VT_FLH) = SMIN(G$(IGCND(G)  AND IAGKNY(Y,IA,G)), VT_FIXED(Y,IA,G) + VT_VAREL(Y,IA,G) * VT_FLH.VAL);
VT_CHEAPESTHOBF(Y,IA,VT_FLH) = SMIN(G$(IGHOB(G)  AND IAGKNY(Y,IA,G)), VT_FIXED(Y,IA,G) + VT_VARHEAT(Y,IA,G) * VT_FLH.VAL);

* identifying the cheapest CND option in the region
VT_CHEAPESTCNDR(Y,IR,VT_FLH) = SMIN(IA$RRRAAA(IR,IA),VT_CHEAPESTCND(Y,IA,VT_FLH));




** Calculating annual costs of serving Heat (H) or Electricity (E) under different assumptions for full load hours, full load hours for heat generation and value of "by-product" (heat when considering el-production and visa versa)
* Extraction plants
VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(IGEXT(G)  AND IAGKNY(Y,IA,G) ) =
    VT_FIXED(Y,IA,G) + VT_VARHEAT(Y,IA,G) * VT_FLHHEAT.VAL   + VT_VAREL(Y,IA,G)* VT_FLH.VAL*GDATA(G,'GDCB')  - VT_PR.VAL/100 * SUM(IR$RRRAAA(IR,IA), VT_CHEAPESTCNDR(Y,IR,VT_FLH))*GDATA(G,'GDCB') ;
VT_CHEAPESTNEWE(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(IGEXT(G)  AND IAGKNY(Y,IA,G)) =
    (VT_FIXED(Y,IA,G) + VT_VAREL(Y,IA,G) * VT_FLH.VAL)  + VT_VARHEAT(Y,IA,G) * VT_FLHHEAT.VAL /GDATA(G,'GDCB')   - VT_PR.VAL/100 * MIN(1000000, VT_CHEAPESTHOB(Y,IA,VT_FLHHEAT)) /GDATA(G,'GDCB')  ;

* Back pressure plants
VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(IGBPR(G)  AND IAGKNY(Y,IA,G) ) = VT_FIXED(Y,IA,G) + VT_VARHEAT(Y,IA,G) * VT_FLH.VAL   + VT_VAREL(Y,IA,G)* VT_FLH.VAL*GDATA(G,'GDCB')  - VT_PR.VAL/100 * SUM(IR$RRRAAA(IR,IA), VT_CHEAPESTCNDR(Y,IR,VT_FLH))*GDATA(G,'GDCB') ;
VT_CHEAPESTNEWE(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(IGBPR(G)  AND IAGKNY(Y,IA,G))  = VT_FIXED(Y,IA,G) + VT_VAREL(Y,IA,G) * VT_FLH.VAL  + VT_VARHEAT(Y,IA,G) * VT_FLH.VAL /GDATA(G,'GDCB') - VT_PR.VAL/100 * MIN(1000000, VT_CHEAPESTHOB(Y,IA,VT_FLHHEAT)) /GDATA(G,'GDCB')  ;


* Condencing / HOB plants
VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(IGHOB(G)  AND IAGKNY(Y,IA,G) ) = VT_FIXED(Y,IA,G) + VT_VARHEAT(Y,IA,G) * VT_FLHHEAT.VAL ;
VT_CHEAPESTNEWE(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(IGCND(G)  AND IAGKNY(Y,IA,G))  = VT_FIXED(Y,IA,G) + VT_VAREL(Y,IA,G) * VT_FLH.VAL;

* Power to heat plants
*VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(IGETOH(G)  AND IAGKNY(Y,IA,G) ) = VT_FIXED(Y,IA,G) + VT_VARHEAT(Y,IA,G) * VT_FLHHEAT.VAL + VT_PR.VAL/100 * SUM(IR$RRRAAA(IR,IA), VT_CHEAPESTCNDR(Y,IR,VT_FLH))/GDATA(G,'GDFE');

VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(VT_FLHHEAT.VAL > VT_FLH.VAL) = 0;
VT_CHEAPESTNEWE(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$(VT_FLHHEAT.VAL > VT_FLH.VAL) = 0;



** Calulating the cost of the cheapest plants
* when not considering plants affected by max and eq fuel restrictions
VT_CHEAPESTNEW1E(Y,IA,VT_FLH,VT_FLHHEAT,VT_PR)= SMIN(G$((IGEXT(G) OR IGBPR(G) OR IGCND(G))
         AND IAGKNY(Y,IA,G) AND NOT VT_REQEMAX(Y,IA,G) AND NOT VT_REQMAX(Y,IA,G) AND NOT VT_REQFX(Y,IA,G)) , VT_CHEAPESTNEWE(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR));
VT_CHEAPESTNEW1H(Y,IA,VT_FLH,VT_FLHHEAT,VT_PR)= SMIN(G$((IGEXT(G) OR IGBPR(G) OR IGHOB(G) OR IGETOH(G))
         AND IAGKNY(Y,IA,G) AND NOT VT_REQEMAX(Y,IA,G) AND NOT VT_REQMAX(Y,IA,G) AND NOT VT_REQFX(Y,IA,G)) , VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR));
* when considering plants affected by max and eq fuel restrictions
VT_CHEAPESTNEW1EF(Y,IA,VT_FLH,VT_FLHHEAT,VT_PR)= SMIN(G$((IGEXT(G) OR IGBPR(G) OR IGCND(G))  AND IAGKNY(Y,IA,G)) ,
         VT_CHEAPESTNEWE(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR));
VT_CHEAPESTNEW1HF(Y,IA,VT_FLH,VT_FLHHEAT,VT_PR)= SMIN(G$((IGEXT(G) OR IGBPR(G) OR IGHOB(G) )  AND IAGKNY(Y,IA,G)) ,
         VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR));

** Identifying the cheapest plant
* when not considering plants affected by max and eq fuel restrictions
VT_CHEAPESTNEW2(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$((IGEXT(G) OR IGBPR(G) OR IGCND(G))  AND IAGKNY(Y,IA,G) AND NOT (VT_FLHHEAT.VAL > VT_FLH.VAL)
                 AND (VT_CHEAPESTNEWE(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR) = VT_CHEAPESTNEW1E(Y,IA,VT_FLH,VT_FLHHEAT,VT_PR)))  = YES;
VT_CHEAPESTNEW2(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$((IGEXT(G) OR IGBPR(G) OR IGHOB(G) )  AND IAGKNY(Y,IA,G) AND NOT (VT_FLHHEAT.VAL > VT_FLH.VAL)
                 AND ( VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR) = VT_CHEAPESTNEW1H(Y,IA,VT_FLH,VT_FLHHEAT,VT_PR))) = YES;
* when considering plants affected by max and eq fuel restrictions
VT_CHEAPESTNEW2(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$((IGEXT(G) OR IGBPR(G) OR IGCND(G))  AND IAGKNY(Y,IA,G) AND NOT (VT_FLHHEAT.VAL > VT_FLH.VAL)
         AND ( VT_CHEAPESTNEWE(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR) = VT_CHEAPESTNEW1EF(Y,IA,VT_FLH,VT_FLHHEAT,VT_PR)) )= YES;
VT_CHEAPESTNEW2(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR)$((IGEXT(G) OR IGBPR(G) OR IGHOB(G) )  AND IAGKNY(Y,IA,G) AND NOT (VT_FLHHEAT.VAL > VT_FLH.VAL)
         AND ( VT_CHEAPESTNEWH(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR) = VT_CHEAPESTNEW1HF(Y,IA,VT_FLH,VT_FLHHEAT,VT_PR)))= YES;


** identifying the plants that are not the cheapest option in any of the cases considered
VT_NEVERCHEAPNEW(Y,IA,G)$(IAGKNY(Y,IA,G) AND (IGEXT(G) OR IGBPR(G) OR IGHOB(G) OR IGCND(G)) AND NOT SUM((VT_FLH,VT_FLHHEAT,VT_PR), VT_CHEAPESTNEW2(Y,IA,G,VT_FLH,VT_FLHHEAT,VT_PR))) = YES;

**************************************
*** Changes to IAGKNY:
**************************************

VT_NEVERCHEAP2(Y,IA,G)=VT_NEVERCHEAPNEW(Y,IA,G);

* Technologies with fx or minimum fuel use requirments are NOT removed from IAGKNY:
VT_NEVERCHEAP2(Y,IA,G)$(VT_REQEMIN(Y,IA,G) OR VT_REQMIN(Y,IA,G) OR VT_REQFX(Y,IA,G)) = NO;


IAGKNY(Y,IA,G)$VT_NEVERCHEAP2(Y,IA,G) = NO;


*execute_unload "../output/temp/costs.gdx", VT_fixed, VT_varel, VT_varheat, VT_CHEAPESTHOBF, VT_REQMIN, VT_REQEMIN, VT_REQMAX, VT_REQEMAX, VT_REQFX, VT_CHEAPESTHOB, VT_CHEAPESTCND, VT_CHEAPESTCNDR,  VT_NEVERCHEAP, VT_NEVERCHEAP2,IAGKNY,
*VT_CHEAPESTEXT0, VT_NEVERCHEAPNEW, VT_CHEAPESTNEW2, VT_CHEAPESTNEWH, VT_CHEAPESTNEWE, VT_CHEAPESTNEW1E, VT_CHEAPESTNEW1H, VT_CHEAPESTNEW1EF, VT_CHEAPESTNEW1HF;
