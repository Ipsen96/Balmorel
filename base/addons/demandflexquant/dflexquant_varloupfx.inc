* File dflexquant_varloupfx.inc for the Balmorel model.
* For use with addon dflexquant
* Latest revision 20171023(hr)

* Elastic demand quantities each time segment, upper bounds on the quantities of the individual steps.

* Using help parameters IDE_T and IDH_T

IDE_T(IY411,IR,DEUSER,S,T) = ((DE(IY411,IR,DEUSER) * DE_VAR_T(IR,DEUSER,S,T)) / IDE_SUMST(IR,DEUSER))$(IDE_SUMST(IR,DEUSER) GT 0);
IDH_T(IY411,IA,DHUSER,S,T) = ((DH(IY411,IA,DHUSER) * DH_VAR_T(IA,DHUSER,S,T)) / IDH_SUMST(IA,DHUSER))$(IDH_SUMST(IA,DHUSER) GT 0);

VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_D1)$(ORD(DEF_D1) LT CARD(DEF_D1)) = IDE_T(IY411,IR,DEUSER,S,T) * (DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_D1+1)-DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_D1));
VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_D1)$((ORD(DEF_D1) EQ CARD(DEF_D1)) AND (1 LT CARD(DEF_D1))) = IDE_T(IY411,IR,DEUSER,S,T) * (1-DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_D1));
VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_U1)$((ORD(DEF_U1) EQ 1) AND (1 LT CARD(DEF_U1))) = IDE_T(IY411,IR,DEUSER,S,T) * (DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_U1)-1);
VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_U1)$(1 LT ORD(DEF_U1)) = IDE_T(IY411,IR,DEUSER,S,T) * (DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_U1)-DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_U1-1));

VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_D2)$(ord(def_d2) eq card(def_d2)) = DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_D2);
VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_D2)$(ord(def_d2) lt card(def_d2)) = DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_D2)- DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_D2+1);
VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_U2) = DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_U2);

VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_U3) = IDE_T(IY411,IR,DEUSER,S,T)*DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_U3);
VDEF_T.UP(IY411,IR,DEUSER,S,T,DEF_D3) = IDE_T(IY411,IR,DEUSER,S,T)*DEF_STEPS(IR,DEUSER,S,T,'DF_QUANT',DEF_D3);

VDHF_T.UP(IY411,IA,DHUSER,S,T,DHF_D1)$(ORD(DHF_D1) EQ 1) = INF;
VDHF_T.UP(IY411,IA,DHUSER,S,T,DHF_D1)$((1 LT ORD(DHF_D1)) AND (ORD(DHF_D1) LT CARD(DHF_D1))) = IDH_T(IY411,IA,DHUSER,S,T) * (DHF_STEPS(IA,DHUSER,S,T,'DF_QUANT',DHF_D1+1)-DHF_STEPS(IA,DHUSER,S,T,'DF_QUANT',DHF_D1));
VDHF_T.UP(IY411,IA,DHUSER,S,T,DHF_D1)$((ORD(DHF_D1) EQ CARD(DHF_D1)) AND (1 LT CARD(DHF_D1)))= IDH_T(IY411,IA,DHUSER,S,T)  *(1-DHF_STEPS(IA,DHUSER,S,T,'DF_QUANT',DHF_D1));
VDHF_T.UP(IY411,IA,DHUSER,S,T,DHF_U1)$((ORD(DHF_U1) EQ 1) AND (1 LT CARD(DHF_D1))) = IDH_T(IY411,IA,DHUSER,S,T) * (DHF_STEPS(IA,DHUSER,S,T,'DF_QUANT',DHF_U1)-1);
VDHF_T.UP(IY411,IA,DHUSER,S,T,DHF_U1)$((1 LT ORD(DHF_U1)) AND (ORD(DHF_U1) LT CARD(DHF_U1))) = IDH_T(IY411,IA,DHUSER,S,T) * (DHF_STEPS(IA,DHUSER,S,T,'DF_QUANT',DHF_U1)-DHF_STEPS(IA,DHUSER,S,T,'DF_QUANT',DHF_U1-1));
VDHF_T.UP(IY411,IA,DHUSER,S,T,DHF_U1)$(ORD(DHF_U1) EQ CARD(DHF_U1))= INF;




