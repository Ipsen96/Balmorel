Equations
   QSTEPWISEPRICE_GFTOSTEP(Y,C,STEPWISEPRICE_FFF)                'Fuel usage in each country fixed to stepwise variable'
   QSTEPWISEPRICE_GFSTEP(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)         'Fuel usage on each line segment does not exceed max limit in energy'
   QSTEPWISEPRICE_GFSTEP_GLOB(Y,STEPWISEPRICE_L,STEPWISEPRICE_FFF)         'Fuel usage on each line segment does not exceed max limit in energy'
   QSTEPWISEPRICE_VGFLEVEL(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)         'Fuel usage in a lower line segment is greater than the next one, except for L7'
;

QSTEPWISEPRICE_GFTOSTEP(IY411,C,STEPWISEPRICE_FFF)$(ISTEPWISEPRICE_F(IY411,C,STEPWISEPRICE_FFF) OR ISTEPWISEPRICE_F_GLOB(IY411,STEPWISEPRICE_FFF) AND SUM((IA,G)$(IGF(G,STEPWISEPRICE_FFF) AND ICA(C,IA)),IAGK_HASORPOT(IY411,IA,G))) ..
     SUM(IA$(ICA(C,IA)
$ifi %INDHEAT%==yes     and not INDHEAT_AAA(IA)
                        ),
$ifi %VGF%==ST         SUM(G$(IAGK_HASORPOT(IY411,IA,G) AND (IGF(G,STEPWISEPRICE_FFF))),SUM((IS3,T), IHOURSINST(IS3,T) * VGF_T(IY411,IA,G,IS3,T)))
$ifi %VGF%==Y         SUM(G$(IAGK_HASORPOT(IY411,IA,G) AND (IGF(G,STEPWISEPRICE_FFF))), VGF_T(IY411,IA,G))
    )
         =e=
    SUM(STEPWISEPRICE_L, VSTEPWISEPRICE_VGF_T_STEP(IY411,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
;

QSTEPWISEPRICE_GFSTEP(IY411,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)$ISTEPWISEPRICE_F(IY411,C,STEPWISEPRICE_FFF)..
   IOF3P6*VSTEPWISEPRICE_VGF_T_STEP(IY411,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)
         =l=
   STEPWISEPRICE_AMOUNT(IY411,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)
;

QSTEPWISEPRICE_GFSTEP_GLOB(IY411,STEPWISEPRICE_L,STEPWISEPRICE_FFF)$ISTEPWISEPRICE_F_GLOB(IY411,STEPWISEPRICE_FFF)..
   SUM(C,IOF3P6*VSTEPWISEPRICE_VGF_T_STEP(IY411,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
         =l=
   STEPWISEPRICE_AMOUNT_GLOB(IY411,STEPWISEPRICE_L,STEPWISEPRICE_FFF)
;

QSTEPWISEPRICE_VGFLEVEL(IY411,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)$(ISTEPWISEPRICE_F_GLOB(IY411,STEPWISEPRICE_FFF) AND (ORD(STEPWISEPRICE_L) LE 7))..
   VSTEPWISEPRICE_VGF_T_STEP(IY411,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)
         =G=
   VSTEPWISEPRICE_VGF_T_STEP(IY411,C,STEPWISEPRICE_L+1,STEPWISEPRICE_FFF)
;


$ifi not %INDHEAT%==yes  $goto noindheateqndef
Equation  QSTEPWISEPRICE_GFTOSTEP_INDHEAT(Y,C,STEPWISEPRICE_FFF);
QSTEPWISEPRICE_GFTOSTEP_INDHEAT(IY411,C,STEPWISEPRICE_FFF)$(ISTEPWISEPRICE_F(IY411,C,STEPWISEPRICE_FFF) OR ISTEPWISEPRICE_F_GLOB(IY411,STEPWISEPRICE_FFF) AND SUM((INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$(INDHEAT_IGF(INDHEAT_G,STEPWISEPRICE_FFF) AND ICA(C,INDHEAT_IA)),INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))) ..
    SUM(INDHEAT_IA$ICA(C,INDHEAT_IA),
$ifi %VGF%==ST         SUM((INDHEAT_G,INDHEAT_GROUP)$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IGF(INDHEAT_G,STEPWISEPRICE_FFF) ),SUM((IS3,T), IHOURSINST(IS3,T) * INDHEAT_VGF_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)))
$ifi %VGF%==Y         SUM((INDHEAT_G,INDHEAT_GROUP)$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IGF(INDHEAT_G,STEPWISEPRICE_FFF) ), INDHEAT_VGF_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))
    )
         =e=
    SUM(STEPWISEPRICE_L, VSTEPWISEPRICE_VGF_T_STEP(IY411,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
;
$label noindheateqndef
