**** AREA Aggregation

ALIAS(AAA,A_ALIAS);
ALIAS(AAA,IA_ALIAS);



SET AREA_AGGREGATION(AAA,A_ALIAS) 'Areas to aggregate'     %semislash%
$if     EXIST '../data/AREA_AGGREGATION.inc' $INCLUDE         '../data/AREA_AGGREGATION.inc';
$if not EXIST '../data/AREA_AGGREGATION.inc' $INCLUDE '../../base/data/AREA_AGGREGATION.inc';
%semislash%;




*** All parameters over the AAA set with changes:


*** AGKN

AGKN(IA,G)$(SUM(A_ALIAS, AREA_AGGREGATION(IA,A_ALIAS))) = AGKN(IA,G) + SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS), AGKN(IA_ALIAS,G));
AGKN(IA_ALIAS,G)$(SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS))) = NO;

*** GKFX

GKFX(Y,IA,G)$(SUM(A_ALIAS, AREA_AGGREGATION(IA,A_ALIAS))) = GKFX(Y,IA,G) + SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS), GKFX(Y,IA_ALIAS,G));
GKFX(Y,IA_ALIAS,G)$SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ) = 0;

*** AGKNDISC  (not currently in use)

*** DHFP_CALIB  (not currently in use)

*** GKNMAX (not currently in use for relevant areas)

*** DISCOST_H, DISLOSS_H, DISLOSS1H (not currently relevant)

*** FKPOT, TECHGROUPKPOT (not currently relevant)

*** TECHGROUPPOT
*** METHOD: SUM

TECHGROUPKPOT(IA,TECH_GROUP)$(SUM(A_ALIAS, AREA_AGGREGATION(IA,A_ALIAS))) = TECHGROUPKPOT(IA,TECH_GROUP) +  SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS), TECHGROUPKPOT(IA_ALIAS,TECH_GROUP));
TECHGROUPKPOT(IA_ALIAS,TECH_GROUP)$SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ) = 0;


*** FGEMIN, FGEMAX (not currently relevant)

*** GMINF, GMAXF, GEQF (not currently relevant)

*** FKMIN, FKMAX, FKEQ, FKMINA, FKMAXNA, FKEQNA, FMAXINVESTNA  (not currently relevant)

*** WTRRSFLH  (not currently relevant)

*** WTRRRFLH (avoided)

*** SOLEFLH  (not currently relevant)

** METHOD: ARITHMETIC MEAN


*** WAVEFLH  (not currently relevant)

*** HYRSMAXVOL_G, HYRSDATA (not currently relevant)

*** GINVCOST  (Through changes in AGKN this parameter updates accordingly)

*** GOMVCOST  (Through changes in AGKN this parameter updates accordingly)

*** GOMFCOST  (Through changes in AGKN this parameter updates accordingly)

*** DECOM_G   (Through changes in AGKN this parameter updates accordingly)

*** GEFFDERATE  (currently not relevant)

*** GKRATE  (currently not relevant)
** METHOD: ARITHMETIC MEAN
GKRATE(IA,G,SSS)$(SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS), GKRATE(IA_ALIAS,G,SSS) )>0)  =
         (GKRATE(IA,G,SSS)$(SUM(A_ALIAS, AREA_AGGREGATION(IA,A_ALIAS)))+  SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS) , GKRATE(IA_ALIAS,G,SSS)  )  )
                 /      (SUM(IA_ALIAS$(AREA_AGGREGATION(IA,IA_ALIAS) AND (GKRATE(IA_ALIAS,G,SSS) >0)), 1)+1$(GKRATE(IA,G,SSS) >0));    !!
GKRATE(IA_ALIAS,G,SSS)$(SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ))  = 0;

*** CHPPROFILES (not currently relevant)

*** DHFP_BASE (data exist, but same for all relevant areas)

*** DH
*Orignial values of DH is saved for later use in DH_VAR_T calculation
PARAMETER AA_DHSUM(AAA,DHUSER);  AA_DHSUM(IA,DHUSER)$(SUM(IA_ALIAS,AREA_AGGREGATION(IA,IA_ALIAS)) OR SUM(IA_ALIAS,AREA_AGGREGATION(IA_ALIAS,IA)))  =    SUM(Y, DH(Y,IA,DHUSER))/CARD(Y);

DH(Y,IA,DHUSER)$(SUM(IA_ALIAS, AREA_AGGREGATION(IA,IA_ALIAS))) = DH(Y,IA,DHUSER) + SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS), DH(Y,IA_ALIAS,DHUSER));
DH(Y,IA_ALIAS,DHUSER)$SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ) = 0;


*** FUELPRICE (currently on region level)

*** WTRRSVAR_S (not currently relevant)

*** WTRRRVAR_S  (not currently relevant)

$ifi %import_results%==YES $GOTO LABEL_AA_SOLH_VAR_T

*** SOLHFLH
*Orignial values of SOLHFLH is saved for later use in SOLH_VAR_T calculation
PARAMETER AA_SOLHFLH(AAA); AA_SOLHFLH(IA)$(SUM(IA_ALIAS,AREA_AGGREGATION(IA,IA_ALIAS)) OR SUM(IA_ALIAS,AREA_AGGREGATION(IA_ALIAS,IA)))  = SOLHFLH(IA);

SOLHFLH(IA)$(SUM(A_ALIAS, AREA_AGGREGATION(IA,A_ALIAS))) =
                 (SOLHFLH(IA) + SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS),   SOLHFLH(IA_ALIAS))) / (SUM(IA_ALIAS$(AREA_AGGREGATION(IA,IA_ALIAS) AND SOLHFLH(IA_ALIAS)), 1)+1$(SOLHFLH(IA) >0));
SOLHFLH(IA_ALIAS)$SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ) = 0;

*** SOLH_VAR_T
PARAMETER AA_SOLHVARTSUM(AAA);
AA_SOLHVARTSUM(IA)$(SUM(IA_ALIAS,AREA_AGGREGATION(IA,IA_ALIAS)) OR SUM(IA_ALIAS,AREA_AGGREGATION(IA_ALIAS,IA)))    = SUM((SSS,TTT), SOLH_VAR_T(IA,SSS,TTT));

SOLH_VAR_T(IA,SSS,TTT)$(SUM(A_ALIAS, AREA_AGGREGATION(IA,A_ALIAS))) =
                ( SOLH_VAR_T(IA,SSS,TTT)*AA_SOLHFLH(IA)/AA_SOLHVARTSUM(IA))$AA_SOLHVARTSUM(IA)
         + SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS),   (SOLH_VAR_T(IA_ALIAS,SSS,TTT)*AA_SOLHFLH(IA_ALIAS)/AA_SOLHVARTSUM(IA_ALIAS))$AA_SOLHVARTSUM(IA_ALIAS));

SOLH_VAR_T(IA_ALIAS,SSS,TTT)$SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ) = 0;
$LABEL LABEL_AA_SOLH_VAR_T

*** SOLE_VAR_T  (not currently relevant)




*** WNDFLH  (not currently relevant)
PARAMETER AA_WNDFLH(AAA); AA_WNDFLH(IA)$(SUM(IA_ALIAS,AREA_AGGREGATION(IA,IA_ALIAS)) OR SUM(IA_ALIAS,AREA_AGGREGATION(IA_ALIAS,IA)))  = WNDFLH(IA);

WNDFLH(IA)$(SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS),WNDFLH(IA_ALIAS))) =
                 (WNDFLH(IA) + SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS),   WNDFLH(IA_ALIAS))) / (SUM(IA_ALIAS$(AREA_AGGREGATION(IA,IA_ALIAS) AND WNDFLH(IA_ALIAS)), 1)+1$(WNDFLH(IA) >0));
WNDFLH(IA_ALIAS)$SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ) = 0;

*** WND_VAR_T
PARAMETER AA_WNDVARTSUM(AAA);
AA_WNDVARTSUM(IA)$(SUM(IA_ALIAS,AREA_AGGREGATION(IA,IA_ALIAS)) OR SUM(IA_ALIAS,AREA_AGGREGATION(IA_ALIAS,IA)))    = SUM((SSS,TTT), WND_VAR_T(IA,SSS,TTT));

WND_VAR_T(IA,SSS,TTT)$(SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS),WND_VAR_T(IA_ALIAS,SSS,TTT))) =
                ( WND_VAR_T(IA,SSS,TTT)*AA_WNDFLH(IA)/AA_WNDVARTSUM(IA))$AA_WNDVARTSUM(IA)
         + SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS),   (WND_VAR_T(IA_ALIAS,SSS,TTT)*AA_WNDFLH(IA_ALIAS)/AA_WNDVARTSUM(IA_ALIAS))$AA_WNDVARTSUM(IA_ALIAS));

WND_VAR_T(IA_ALIAS,SSS,TTT)$SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ) = 0;


*** WAVE_VAR_T  (not currently relevant)

*** HYPPROFILS (not currently relevant)

*** DHF_STEPS (not currently relevant)

*** DH_VAR_T
*** METHOD: Mean weighted to average DH consumption in the years in Y

PARAMETER AA_DHVARTSUM(AAA,DHUSER);
AA_DHVARTSUM(IA,DHUSER)$(SUM(IA_ALIAS,AREA_AGGREGATION(IA,IA_ALIAS)) OR SUM(IA_ALIAS,AREA_AGGREGATION(IA_ALIAS,IA)))    = SUM((SSS,TTT), DH_VAR_T(IA,DHUSER,SSS,TTT));

DH_VAR_T(IA,DHUSER,SSS,TTT)$(SUM(A_ALIAS, AREA_AGGREGATION(IA,A_ALIAS))) =
                 (DH_VAR_T(IA,DHUSER,SSS,TTT)*AA_DHSUM(IA,DHUSER)/AA_DHVARTSUM(IA,DHUSER))$AA_DHVARTSUM(IA,DHUSER)
         + SUM(IA_ALIAS$AREA_AGGREGATION(IA,IA_ALIAS),   (DH_VAR_T(IA_ALIAS,DHUSER,SSS,TTT)*AA_DHSUM(IA_ALIAS,DHUSER)/AA_DHVARTSUM(IA_ALIAS,DHUSER))$AA_DHVARTSUM(IA_ALIAS,DHUSER));

DH_VAR_T(IA_ALIAS,DHUSER,SSS,TTT)$SUM(IA, AREA_AGGREGATION(IA,IA_ALIAS) ) = 0;














