Equations
HEATSV_QHS(YYY,AAA,HEATSV_STEPS)  'Heat savings in an area less than potential, MWh'
HEATSV_QHSACCUMNET(YYY,AAA,HEATSV_STEPS)  'Fixing accumulation of savings'
HEATSV_QEXPMAX(YYY,AAA,HEATSV_STEPS) 'Maximum of heat savings per time step summed to accumulated capacity'
HEATSV_QHSPERCENT_T(YYY,AAA,HEATSV_STEPS,SSS,TTT) 'Percentage of demand fixed per timestep'
$ifi not %INDHEAT%==yes $goto endINDHEATdec
HEATSV_QINDHEATHS(YYY,INDHEAT_AAA,INDHEAT_GROUP,HEATSV_STEPS)  'Heat savings in an individual heating area, MW'
HEATSV_QINDHEATHSGROUPTOACCUM(YYY,INDHEAT_AAA,HEATSV_STEPS)
HEATSV_QINDHEATHSACCUMNET(YYY,INDHEAT_AAA,HEATSV_STEPS)
HEATSV_QINDHEATEXPMAX(YYY,INDHEAT_AAA,INDHEAT_GROUP,HEATSV_STEPS)
HEATSV_QINDHEATHSPERCENT_T(YYY,INDHEAT_AAA,INDHEAT_GROUP,HEATSV_STEPS,SSS,TTT)
$label endINDHEATdec
HEATSV_QEQSAV(YYY,CCC)   'Heat savings equal to a fixed number'
;

HEATSV_QHS(IY411,IA,HEATSV_STEPS)$HEATSV_POT(IY411,IA,HEATSV_STEPS)..

        HEATSV_VHSACCUMNET(IY411,IA,HEATSV_STEPS) =l= HEATSV_POT(IY411,IA,HEATSV_STEPS);

HEATSV_QHSACCUMNET(IY411,IA,HEATSV_STEPS)$HEATSV_POT(IY411,IA,HEATSV_STEPS) ..
         HEATSV_VHSACCUMNET(IY411,IA,HEATSV_STEPS)
         =e=
         SUM(IY402$IY411PREVY(IY411,IY402),HEATSV_VHSACCUMNET(IY402,IA,HEATSV_STEPS))$(NOT IYFIRST(IY411))
         + HEATSV_VHS(IY411,IA,HEATSV_STEPS)
;


HEATSV_QEXPMAX(IY411,IA,HEATSV_STEPS)$HEATSV_POT(IY411,IA,HEATSV_STEPS) ..
         SUM((IS3,T),IHOURSINST(IS3,T) * HEATSV_VHS_T(IY411,IA,HEATSV_STEPS,IS3,T))
         =l=
         HEATSV_VHSACCUMNET(IY411,IA,HEATSV_STEPS)
;

HEATSV_QHSPERCENT_T(IY411,IA,HEATSV_STEPS,IS3,T)$HEATSV_POT(IY411,IA,HEATSV_STEPS) ..
         HEATSV_VHS_T(IY411,IA,HEATSV_STEPS,IS3,T) =e= HEATSV_VHSPERCENT(IY411,IA,HEATSV_STEPS)*DH_VAR_T(IA,'RESH',IS3,T)/DH(IY411,IA,'RESH')
;

HEATSV_QEQSAV(IY411,C)$(HEATSV_SAVEQ(IY411,C) AND ( SUM((IA,HEATSV_STEPS)$(ICA(C,IA)),HEATSV_POT(IY411,IA,HEATSV_STEPS))
$ifi not %INDHEATSAV%==yes $goto endINDHEATQEQSAV
         OR SUM((INDHEAT_IA,HEATSV_STEPS)$(ICA(C,INDHEAT_IA)),HEATSV_INDHEATPOT(IY411,INDHEAT_IA,HEATSV_STEPS))
$label endINDHEATQEQSAV
         )) ..
         SUM((IA,HEATSV_STEPS)$(ICA(C,IA)),HEATSV_VHSACCUMNET(IY411,IA,HEATSV_STEPS))
$ifi not %INDHEATSAV%==yes $goto endINDHEATQEQSAVdef
         + SUM((INDHEAT_IA,HEATSV_STEPS)$(ICA(C,INDHEAT_IA)),HEATSV_VINDHEATHSACCUMNET(IY411,INDHEAT_IA,HEATSV_STEPS))
$label endINDHEATQEQSAVdef
         =E=
         HEATSV_SAVEQ(IY411,C)
;


$ifi not %INDHEATSAV%==yes $goto endINDHEATdef
HEATSV_QINDHEATHS(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS)$HEATSV_INDHEATPOT(IY411,INDHEAT_IA,HEATSV_STEPS)..
        HEATSV_VINDHEATHSGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS) =l= HEATSV_INDHEATPOT(IY411,INDHEAT_IA,HEATSV_STEPS)*INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP)
;

HEATSV_QINDHEATHSGROUPTOACCUM(IY411,INDHEAT_IA,HEATSV_STEPS)$HEATSV_INDHEATPOT(IY411,INDHEAT_IA,HEATSV_STEPS) ..
         SUM(INDHEAT_GROUP,HEATSV_VINDHEATHSGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS))
         =e=
         HEATSV_VINDHEATHSACCUMNET(IY411,INDHEAT_IA,HEATSV_STEPS)
;

HEATSV_QINDHEATHSACCUMNET(IY411,INDHEAT_IA,HEATSV_STEPS)$HEATSV_INDHEATPOT(IY411,INDHEAT_IA,HEATSV_STEPS) ..
         HEATSV_VINDHEATHSACCUMNET(IY411,INDHEAT_IA,HEATSV_STEPS)
         =e=
         SUM(IY402$IY411PREVY(IY411,IY402),HEATSV_VINDHEATHSACCUMNET(IY402,INDHEAT_IA,HEATSV_STEPS))$(NOT IYFIRST(IY411))
         + HEATSV_VINDHEATHS(IY411,INDHEAT_IA,HEATSV_STEPS)
;

HEATSV_QINDHEATEXPMAX(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS)$HEATSV_INDHEATPOT(IY411,INDHEAT_IA,HEATSV_STEPS) ..
         SUM((IS3,T),IHOURSINST(IS3,T) * HEATSV_VINDHEATHS_T(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS,IS3,T))
         =e=
         HEATSV_VINDHEATHSGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS)
;

HEATSV_QINDHEATHSPERCENT_T(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS,IS3,T)$HEATSV_INDHEATPOT(IY411,INDHEAT_IA,HEATSV_STEPS) ..
         HEATSV_VINDHEATHS_T(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS,IS3,T) =e=
         HEATSV_VINDHEATHSPERCENT(IY411,INDHEAT_IA,INDHEAT_GROUP,HEATSV_STEPS)*INDHEAT_DH_VAR_T(INDHEAT_IA,'SPACEHEAT',IS3,T)/INDHEAT_DH(IY411,INDHEAT_IA,'SPACEHEAT')
;
$label endINDHEATdef
