
EQUATIONS

QRAMPING_UP_CAPABILITY (Y,AAA,G,S,T)    'Equation limiting the ramping up capability of a generation technology for each timestep'
QRAMPING_DOWN_CAPABILITY (Y,AAA,G,S,T) 'Equation limiting the ramping down capability of a generation technology for each timestep'
QRAMPING_UP_BALANCE (Y,AAA,G,S,T)       'Equation that balances the total ramping up for each timestep'
QRAMPING_DOWN_BALANCE (Y,AAA,G,S,T)   'Equation that balances the total ramping down for each timestep'
QRAMPING_UP_RATE_CAPABILITY(Y,AAA,G,RAMPING_RATE,S,T)   'Equation that limits the ramping up of each generation technologys ramping rate for each timestep'
QRAMPING_DOWN_RATE_CAPABILITY(Y,AAA,G,RAMPING_RATE,S,T) 'Equation that limits the ramping down of each generation technologys ramping rate for each timestep'
;


QRAMPING_UP_CAPABILITY (IY411,IA,G,IS3,T)$((NOT (ORD(T) EQ CARD(T))) AND IAGK_HASORPOT(IY411,IA,G) AND GDATA(G,'GDRAMPUP') AND (GDATA(G,'GDRAMPUP')*CHRONOHOUR(IS3,T) LT 1))..
VGE_T(IY411,IA,G,IS3,T+1) - VGE_T(IY411,IA,G,IS3,T)
=L=
GDATA(G,'GDRAMPUP')*(GKFX(IY411,IA,G)$IAGKFX(IY411,IA,G)+VGKNACCUMNET(IY411,IA,G)$(IAGK_HASORPOT(IY411,IA,G) AND IAGKN(IA,G)))*CHRONOHOUR(IS3,T)
;

QRAMPING_DOWN_CAPABILITY (IY411,IA,G,IS3,T)$((NOT (ORD(T) EQ CARD(T))) AND IAGK_HASORPOT(IY411,IA,G) AND GDATA(G,'GDRAMPDOWN') AND (GDATA(G,'GDRAMPDOWN')*CHRONOHOUR(IS3,T) LT 1))..
VGE_T(IY411,IA,G,IS3,T) - VGE_T(IY411,IA,G,IS3,T+1)
=L=
GDATA(G,'GDRAMPDOWN')*(GKFX(IY411,IA,G)$IAGKFX(IY411,IA,G)+VGKNACCUMNET(IY411,IA,G)$(IAGK_HASORPOT(IY411,IA,G) AND IAGKN(IA,G)))*CHRONOHOUR(IS3,T)
;

QRAMPING_UP_BALANCE (IY411,IA,G,IS3,T)$((NOT (ORD(T) EQ CARD(T))) AND IRAMPING_G(G) AND IAGK_HASORPOT(IY411,IA,G))..
SUM(RAMPING_RATE,V_RAMPING_UP(IY411,IA,G,RAMPING_RATE,IS3,T)$IRAMPING_G_CAPABILITY_UP(G,RAMPING_RATE))
=G=
VGE_T(IY411,IA,G,IS3,T+1) - VGE_T(IY411,IA,G,IS3,T)
;

QRAMPING_DOWN_BALANCE (IY411,IA,G,IS3,T)$((NOT (ORD(T) EQ CARD(T))) AND IRAMPING_G(G) AND IAGK_HASORPOT(IY411,IA,G))..
SUM(RAMPING_RATE,V_RAMPING_DOWN(IY411,IA,G,RAMPING_RATE,IS3,T)$IRAMPING_G_CAPABILITY_DOWN(G,RAMPING_RATE))
=G=
VGE_T(IY411,IA,G,IS3,T) - VGE_T(IY411,IA,G,IS3,T+1)
;

QRAMPING_UP_RATE_CAPABILITY (IY411,IA,G,RAMPING_RATE,IS3,T)$((NOT (ORD(T) EQ CARD(T))) AND IRAMPING_G_RATE(G,RAMPING_RATE) AND IAGK_HASORPOT(IY411,IA,G) AND IRAMPING_G_CAPABILITY_UP(G,RAMPING_RATE))..
V_RAMPING_UP(IY411,IA,G,RAMPING_RATE,IS3,T)
=L=
IRAMPING_G_CAPABILITY_UP(G,RAMPING_RATE)*(GKFX(IY411,IA,G)$IAGKFX(IY411,IA,G)+VGKNACCUMNET(IY411,IA,G)$(IAGK_HASORPOT(IY411,IA,G) AND IAGKN(IA,G)))*CHRONOHOUR(IS3,T)
;

QRAMPING_DOWN_RATE_CAPABILITY (IY411,IA,G,RAMPING_RATE,IS3,T)$((NOT (ORD(T) EQ CARD(T))) AND IRAMPING_G_RATE(G,RAMPING_RATE) AND IAGK_HASORPOT(IY411,IA,G) AND IRAMPING_G_CAPABILITY_DOWN(G,RAMPING_RATE))..
V_RAMPING_DOWN(IY411,IA,G,RAMPING_RATE,IS3,T)
=L=
IRAMPING_G_CAPABILITY_DOWN(G,RAMPING_RATE)*(GKFX(IY411,IA,G)$IAGKFX(IY411,IA,G)+VGKNACCUMNET(IY411,IA,G)$(IAGK_HASORPOT(IY411,IA,G) AND IAGKN(IA,G)))*CHRONOHOUR(IS3,T)
;




