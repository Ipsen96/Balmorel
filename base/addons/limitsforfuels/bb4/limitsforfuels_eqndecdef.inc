Equations
   QLIMITSFORFUELS_GMAXCF(Y,C,LIMITSFORFUELS_NUMCONMAX)       'Maximum fuel usage per country for a shared max for various fuels'
   QLIMITSFORFUELS_GMINAF(Y,AAA,LIMITSFORFUELS_NUMCONMIN)     'Minimum fuel usage per area for a shared min for various fuels'
;

QLIMITSFORFUELS_GMAXCF(IY411,C,LIMITSFORFUELS_INUMCONMAXC)$(SUM(FFF$(LIMITSFORFUELS_GMAXFSET(LIMITSFORFUELS_INUMCONMAXC,C,FFF)),LIMITSFORFUELS_GMAXF(IY411,C,FFF)) >0)..
    SUM(FFF$(LIMITSFORFUELS_GMAXFSET(LIMITSFORFUELS_INUMCONMAXC,C,FFF)),LIMITSFORFUELS_GMAXF(IY411,C,FFF)
*    + VLIMITSFORFUELS_QGMAXCF(IY411,C,FFF,LIMITSFORFUELS_INUMCONMAXC,'IPLUS')
         )
         =G=
    SUM(IA$ICA(C,IA),
     SUM(FFF$LIMITSFORFUELS_VGFMAXSET(LIMITSFORFUELS_INUMCONMAXC,C,FFF),
     SUM(G$(IAGK_HASORPOT(IY411,IA,G) AND (GDATA(G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM'))), 1/(1$(LIMITSFORFUELS_CONVFACTORMAX(FFF)=0)+LIMITSFORFUELS_CONVFACTORMAX(FFF))*IOF3P6 * SUM((IS3,T), IHOURSINST(IS3,T) * VGF_T(IY411,IA,G,IS3,T)))
     )
    )
$ifi not %INDHEAT%==yes $goto endINDHEAT
         +
         SUM(
         (INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,FFF)$(LIMITSFORFUELS_VGFMAXSET(LIMITSFORFUELS_INUMCONMAXC,C,FFF) AND ICA(C,INDHEAT_IA) AND INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM') AND ( INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP)
$ifi not %COMBTECH%==yes $goto endcombtechCO21
         OR SUM(INDHEAT_GALIAS,INDHEAT_GGCOMB(INDHEAT_GALIAS,INDHEAT_G))
$label endcombtechCO21
         )
         AND (INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
$ifi not %COMBTECH%==yes $goto endcombtechCO22
         OR SUM(INDHEAT_GALIAS$INDHEAT_GGCOMB(INDHEAT_GALIAS,INDHEAT_G), INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_GALIAS,INDHEAT_GROUP))
$label endcombtechCO22
         )
         ),
         SUM( (IS3,T), 1/(1$(LIMITSFORFUELS_CONVFACTORMAX(FFF)=0)+LIMITSFORFUELS_CONVFACTORMAX(FFF))*IHOURSINST(IS3,T) * IOF3P6 * INDHEAT_VGF_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T))
         )
$label endINDHEAT
;

QLIMITSFORFUELS_GMINAF(IY411,IA,LIMITSFORFUELS_INUMCONMINA)$(SUM(FFF$(LIMITSFORFUELS_GMINFSET(LIMITSFORFUELS_INUMCONMINA,IA,FFF)),LIMITSFORFUELS_GMINF(IY411,IA,FFF)) >0)..
    SUM(FFF$(LIMITSFORFUELS_GMINFSET(LIMITSFORFUELS_INUMCONMINA,IA,FFF)),LIMITSFORFUELS_GMINF(IY411,IA,FFF)
    - VLIMITSFORFUELS_QGMINAF(IY411,IA,FFF,LIMITSFORFUELS_INUMCONMINA,'IPLUS')
         )
         =L=
     SUM(FFF$LIMITSFORFUELS_VGFMINSET(LIMITSFORFUELS_INUMCONMINA,IA,FFF),
     SUM(G$(
         (IAGK_HASORPOT(IY411,IA,G)
$ifi not %COMBTECH%==yes $goto nocombtech
         OR SUM(IGALIAS$GGCOMB(IGALIAS,G),IAGK_HASORPOT(IY411,IA,IGALIAS))
$label nocombtech
         )
         AND (GDATA(G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM'))), 1/(1$(LIMITSFORFUELS_CONVFACTORMIN(FFF)=0)+LIMITSFORFUELS_CONVFACTORMIN(FFF))*IOF3P6 * SUM((IS3,T), IHOURSINST(IS3,T) * VGF_T(IY411,IA,G,IS3,T)))
     );
