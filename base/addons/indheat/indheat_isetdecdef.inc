SETS
INDHEAT_IA(INDHEAT_AAA)

INDHEAT_IGF(INDHEAT_G,FFF)
INDHEAT_IGHOB(INDHEAT_G)
INDHEAT_IGETOH(INDHEAT_G)
INDHEAT_IGHP(INDHEAT_G)
INDHEAT_IGETOH_NOTHP(INDHEAT_G)
INDHEAT_IGSOLH(INDHEAT_G)
INDHEAT_IGKRATE(INDHEAT_AAA,INDHEAT_G,S,T)
INDHEAT_IAGKFX(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP)
INDHEAT_IAGK_HASORPOT(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP)

INDHEAT_IAGKN(INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP)
INDHEAT_IAGKNY(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_IA(INDHEAT_AAA) = YES$(SUM(C,ICA(C,INDHEAT_AAA)));
INDHEAT_IGF(INDHEAT_G,FFF)=YES$(INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM'));
INDHEAT_IGHOB(INDHEAT_G) = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GHOB);
INDHEAT_IGETOH(INDHEAT_G) = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GETOH);
INDHEAT_IGHP(INDHEAT_G) = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GETOH AND INDHEAT_GDATA(INDHEAT_G,'GDFE')>1.5);
INDHEAT_IGETOH_NOTHP(INDHEAT_G) =  INDHEAT_IGETOH(INDHEAT_G)- INDHEAT_IGHP(INDHEAT_G);
INDHEAT_IGSOLH(INDHEAT_G) = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GSOLH);
INDHEAT_IGKRATE(INDHEAT_AAA,INDHEAT_G,S,T) = 1;


* ----------------- Assign IAGKFX, IAGKN, IAGKNY, IAGK_HASORPOT ----------------------------------

INDHEAT_IAGKFX(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) = YES$INDHEAT_GKFX(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP);
INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) = YES$INDHEAT_GKFX(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP);

INDHEAT_IAGKN(INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)=NO;
INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)=NO;

$ifi not %TechInvest%==yes $goto Technology_investments_end

* IAGKN and IAGKNY indicates when and where technologies may be invested, based on various combinations of input data.
* Initially: identical to AGKN:
INDHEAT_IAGKN(INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) = INDHEAT_AGKN(INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP);

INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) = INDHEAT_IAGKN(INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP);
INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$(YVALUE(Y) LT INDHEAT_GDATA(INDHEAT_G,'GDFROMYEAR') OR (YVALUE(Y) GT INDHEAT_GDATA(INDHEAT_G,'GDLASTYEAR')) AND (INDHEAT_GDATA(INDHEAT_G,'GDLASTYEAR') GT 0) OR (YVALUE(Y) LT INDHEAT_GINVESTSTART))=NO;


*Update of IAGK_HASORPOT based on potential production

INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) = YES;

*This loop makes sure that if you invest in a technology in the year Y, you can potentially use it in the future, even if you do not allow for investments on this technology in the future
LOOP(Y$(ORD(Y) NE CARD(Y)),
   LOOP(INDHEAT_IA,
      LOOP((INDHEAT_G,INDHEAT_GROUP)$INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP),
         LOOP(IYALIAS2$(ORD(IYALIAS2) GT ORD(Y)),
            INDHEAT_IAGK_HASORPOT(IYALIAS2,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)= INDHEAT_IAGK_HASORPOT(IYALIAS2,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)+INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP);
       );
    );
  );
);


$ifi %COMBTECH%==yes INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,IGCOMB2,INDHEAT_GROUP)$SUM(IGCOMB1$(INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,IGCOMB1,INDHEAT_GROUP) and GGCOMB(IGCOMB1,IGCOMB2)),1)=YES;

$label  Technology_investments_end