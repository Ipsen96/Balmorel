EQUATIONS
INDHEAT_QHEQ(Y,INDHEAT_AAA,INDHEAT_GROUP,S,T) 'Satisfying heat demand'
INDHEAT_QSUMCAPFROM(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Capacity moved away from group'
INDHEAT_QMAXCAPFROM(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP) 'Max moved away less than installed'
INDHEAT_QSUMCAPTO(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Capacity moved to group'
INDHEAT_QGKH_UP(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Max heat generated restricted by installed capacity'
INDHEAT_QGKNACCUMNET(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP) 'Total new installed capacity in current year'
INDHEAT_QGFEQ(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Fuel usage equation'
INDHEAT_QSUMPERCENT(Y,INDHEAT_AAA) 'All demand covered'
INDHEAT_QMINPERCENT(Y,INDHEAT_AAA,INDHEAT_GROUP) 'Minimum percentage of demand generated by group'
INDHEAT_QOUTEQIN(Y,INDHEAT_AAA,INDHEAT_G) 'Moved of technology must be absorbed'
INDHEAT_QGMINPERCENT(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP) 'Minimum percentage provided by a technology in group'
INDHEAT_QSOLHDISPATCH(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Solar heating dispatch'
;


INDHEAT_QHEQ(IY411,INDHEAT_IA,INDHEAT_GROUP,IS3,T)$(INDHEAT_IDH_SUMST(INDHEAT_IA) AND SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)) ) ..
         SUM(INDHEAT_G$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP),
           INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
         )
         =e=
         INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP)*
         (INDHEAT_DH(IY411,INDHEAT_IA) * INDHEAT_DH_VAR_T(INDHEAT_IA,IS3,T)) / (INDHEAT_IDH_SUMST(INDHEAT_IA)$(INDHEAT_IDH_SUMST(INDHEAT_IA) GT 0))
;

INDHEAT_QSUMCAPFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) ..
         SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP, INDHEAT_GROUPTO,IS3,T))
         =l=
         INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_QMAXCAPFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) ..
         INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         =l=
         (
*        INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
*         - INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         + INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))$(SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)))
;

INDHEAT_QSUMCAPTO(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) ..
         SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP,IS3,T))
         =l=
         INDHEAT_VGKNMOVETO(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_QGKH_UP(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)))..
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP,IS3,T))
         =l=
         INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         + INDHEAT_VGKNMOVETO(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         - INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         + INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_QGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GDATA(INDHEAT_G,'GDKVARIABL') EQ 1 AND SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))) ..
         INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         =e=
         SUM(IY402$IY411PREVY(IY411,IY402),INDHEAT_VGKNACCUMNET(IY402,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))
         + INDHEAT_VGKN(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_QGFEQ(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)) ..
         INDHEAT_VGF_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)
         =e=
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)/INDHEAT_GDATA(INDHEAT_G,'GDFE')
;

INDHEAT_QSUMPERCENT(IY411,INDHEAT_IA) ..
         SUM(INDHEAT_GROUP$(SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))), INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP))
         =e=
         1
;

INDHEAT_QMINPERCENT(IY411,INDHEAT_IA,INDHEAT_GROUP) ..
         INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP)
         =g=
         INDHEAT_MINPERCENT(IY411,INDHEAT_IA,INDHEAT_GROUP)
;

INDHEAT_QOUTEQIN(IY411,INDHEAT_IA,INDHEAT_G) ..
         SUM(INDHEAT_GROUP$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP),INDHEAT_VGKNMOVETO(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))
         =e=
         SUM(INDHEAT_GROUP$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP),INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))
;

INDHEAT_QGMINPERCENT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) ..
         SUM((IS3,T),
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
         )
         =g=
         0.05*
         SUM((INDHEAT_GALIAS,IS3,T)$INDHEAT_GINGROUP(INDHEAT_GALIAS,INDHEAT_GROUP),
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_GALIAS,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_GALIAS,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_GALIAS,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
         )
;

INDHEAT_QSOLHDISPATCH(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP,IS3,T)$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP) AND SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)) AND ISOLHSUMST(INDHEAT_IA) AND INDHEAT_GINGROUP(INDHEAT_IGSOLH,INDHEAT_GROUP))..
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP,IS3,T)

$ifi     %VRESHUTDOWN%==yes  =L=
$ifi not %VRESHUTDOWN%==yes  =E=

$ifi %SOLHFLH_DOL%==AAA       (SOLHFLH(INDHEAT_IA) * (
$ifi %SOLHFLH_DOL%==AAA_GGG   (SOLHFLH(INDHEAT_IA,INDHEAT_IGSOLH) * (
       INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)
         + INDHEAT_VGKNMOVETO(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)
         - INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)
         + INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)
                            ) * SOLH_VAR_T(INDHEAT_IA,IS3,T) * INDHEAT_IGKRATE(INDHEAT_IA,INDHEAT_IGSOLH,IS3,T))/ ISOLHSUMST(INDHEAT_IA)
;
