EQUATIONS
INDHEAT_QHEQDHUSER(Y,INDHEAT_AAA,INDHEAT_DHUSER,INDHEAT_GROUP,S,T) 'Satisfying heat demand for user group'
INDHEAT_QSUMCAPFROM(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Capacity moved away from group'
INDHEAT_QMAXCAPFROM(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP) 'Max moved away less than installed times fixed value'
INDHEAT_QSUMCAPTO(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Capacity moved to group'
INDHEAT_QGKH_UP(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Max heat generated restricted by installed capacity'
INDHEAT_QGKNACCUMNET(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP) 'Total new installed capacity in current year'
INDHEAT_QGFEQ(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Fuel usage equation'
INDHEAT_QSUMPERCENT(Y,INDHEAT_AAA) 'All demand covered'
INDHEAT_QMINPERCENT(Y,INDHEAT_AAA,INDHEAT_GROUP) 'Minimum percentage of demand generated by group from year to year'
INDHEAT_QMAXPERCENT(Y,INDHEAT_AAA,INDHEAT_GROUP) 'Maximum percentage of demand generated by group within a year'
INDHEAT_QOUTEQIN(Y,INDHEAT_AAA,INDHEAT_G) 'Moved of technology must be absorbed'
INDHEAT_QGMINPERCENT(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP) 'Minimum percentage provided by a technology in group'
INDHEAT_QSOLHDISPATCH(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Solar heating dispatch'
INDHEAT_QMINPERCENTSUM(Y,INDHEAT_AAA,INDHEAT_SUMGROUP) 'Minimum percentage of demand generated by a summed group from year to year'
INDHEAT_QMINPERCENTSUM2(Y,INDHEAT_AAA,INDHEAT_SUMGROUP) 'Minimum percentage of demand generated by a summed group within a year'
INDHEAT_QMAXPERCENTSUM(Y,INDHEAT_AAA,INDHEAT_SUMGROUP) 'Maximum percentage of demand generated by a summed group within a year'


*Storage equations
INDHEAT_QHSTOVOLT(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Heat storage dynamic equation, MWh'
INDHEAT_QHSTOLOADT(Y,INDHEAT_AAA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T) 'Intra-seasonal Heat storage loading less than heat production from allowed technologies (MW)'
INDHEAT_QHSTOLOADTLIM(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Upper limit to heat storage loading'
INDHEAT_QHSTOVGHTLIM(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Intra-seasonal Heat storage output limit (MW)'
INDHEAT_QHSTOVOLTLIM(Y,INDHEAT_AAA,INDHEAT_G,INDHEAT_GROUP,S,T) 'Heat storage capacity limit (MWh)'
INDHEAT_QGKSTO(Y,INDHEAT_AAA,INDHEAT_GROUP) 'Storage capacity equal to share of generation capacity in the group'
INDHEAT_QHSTOGROUP(Y,INDHEAT_AAA,INDHEAT_DHUSER,INDHEAT_GROUP) 'Storage generation in a group is less than the demand satisfied by the group'
INDHEAT_QHSTOVGHSLIM(Y,INDHEAT_AAA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S)  'Intra-seasonal Heat storage output limited by heat generation in a season(MWh)'
;


INDHEAT_QHEQDHUSER(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GROUP,IS3,T)$(INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER) AND SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)) ) ..
         SUM(INDHEAT_G$(INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER) AND INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)),
           INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
         - SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP, INDHEAT_GROUPTO,IS3,T))
$ifi not %COMBTECH%==yes $goto endcombtechQHEQDHUSER
         + SUM(INDHEAT_GALIAS$INDHEAT_GGCOMB(INDHEAT_G,INDHEAT_GALIAS),
$ifi not %HYDROGEN1%==yes $goto endhydrogenQHEQDHUSER
         (1+INDHEAT_HYDROGEN_H2TOF(INDHEAT_GALIAS)$(INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ THERMGAS_H2 OR INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ BIOGAS_H2))*
$label endhydrogenQHEQDHUSER
         (INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
         - SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP, INDHEAT_GROUPTO,IS3,T))
         ))
$label endcombtechQHEQDHUSER
         )
         - SUM(INDHEAT_IGHSTO$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_GROUP) AND INDHEAT_IGROUPSTO(INDHEAT_GROUP)),INDHEAT_VHSTOLOADT(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_GROUP,IS3,T)*INDHEAT_GTODHUSER(INDHEAT_IGHSTO,INDHEAT_DHUSER))
         =e=
         INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP)*
         (INDHEAT_DH(IY411,INDHEAT_IA,INDHEAT_DHUSER) * INDHEAT_DH_VAR_T(INDHEAT_IA,INDHEAT_DHUSER,IS3,T)) / (INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER)$(INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER) GT 0)+1$(INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER) EQ 0))
;

INDHEAT_QSUMCAPFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) and SUM(INDHEAT_GROUPTO, INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO))) ..
         SUM(INDHEAT_DHUSER$INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER),
         SUM(INDHEAT_GROUPTO$(INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO)), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP, INDHEAT_GROUPTO,IS3,T))
$ifi not %COMBTECH%==yes $goto endcombtechSUMCAPFROM
         +SUM((INDHEAT_GALIAS,INDHEAT_GROUPTO)$(INDHEAT_GGCOMB(INDHEAT_G,INDHEAT_GALIAS) AND INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO)),
$ifi not %HYDROGEN1%==yes $goto endhydrogenSUMCAPFROM
         (1+INDHEAT_HYDROGEN_H2TOF(INDHEAT_GALIAS)$(INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ THERMGAS_H2 OR INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ BIOGAS_H2))*
$label endhydrogenSUMCAPFROM
         INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP, INDHEAT_GROUPTO,IS3,T))
$label endcombtechSUMCAPFROM
         )
         =l=
         INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_QMAXCAPFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) and SUM(INDHEAT_GROUPTO, INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO)) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) ) ..
         INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         =l=
         (
         SUM(IY402$IY411PREVY(IY411,IY402),INDHEAT_VGKNACCUMNET(IY402,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))$(NOT IYFIRST(IY411))
         + INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))
;

INDHEAT_QSUMCAPTO(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$(SUM(INDHEAT_GROUPFROM,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUPFROM)) AND INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) and SUM(INDHEAT_GROUPFROM, INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP))) ..
         SUM(INDHEAT_DHUSER$INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER),
         SUM(INDHEAT_GROUPFROM$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUPFROM) AND INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP)), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP,IS3,T))
$ifi not %COMBTECH%==yes $goto endcombtechSUMCAPTO
         + SUM((INDHEAT_GALIAS,INDHEAT_GROUPFROM)$(INDHEAT_GGCOMB(INDHEAT_G,INDHEAT_GALIAS) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUPFROM) AND INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP)),
$ifi not %HYDROGEN1%==yes $goto endhydrogenSUMCAPTO
         (1+INDHEAT_HYDROGEN_H2TOF(INDHEAT_GALIAS)$(INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ THERMGAS_H2 OR INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ BIOGAS_H2))*
$label endhydrogenSUMCAPTO
         INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUPFROM,INDHEAT_GROUP,IS3,T))
$label endcombtechSUMCAPTO
         )
         =l=
         INDHEAT_VGKNMOVETO(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_QGKH_UP(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) )..
         SUM(INDHEAT_DHUSER$INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER),
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP,IS3,T))
         - SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO,IS3,T))
$ifi not %COMBTECH%==yes $goto endcombtechQGKH
         + SUM(INDHEAT_GALIAS$INDHEAT_GGCOMB(INDHEAT_G,INDHEAT_GALIAS),
$ifi not %HYDROGEN1%==yes $goto endhydrogenQGKHUP
         (1+INDHEAT_HYDROGEN_H2TOF(INDHEAT_GALIAS)$(INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ THERMGAS_H2 OR INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ BIOGAS_H2))*
$label endhydrogenQGKHUP
                 (INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,IS3,T)
                 + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUPFROM,INDHEAT_GROUP,IS3,T))
                 - SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,INDHEAT_GROUPTO,IS3,T)))
         )
$label endcombtechQGKH
         )
         =l=
         INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         + INDHEAT_VGKNMOVETO(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$SUM(INDHEAT_GROUPFROM,INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP))
         - INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$SUM(INDHEAT_GROUPTO,INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO))
         + INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_QGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GDATA(INDHEAT_G,'GDKVARIABL') EQ 1 AND SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))) ..
         INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
         =e=
         SUM(IY402$IY411PREVY(IY411,IY402),INDHEAT_VGKNACCUMNET(IY402,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))$(NOT IYFIRST(IY411))
         + INDHEAT_VGKN(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
;

INDHEAT_QGFEQ(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
$ifi not %COMBTECH%==yes $goto endcombtechQGFEQ
         OR SUM(INDHEAT_GALIAS$(INDHEAT_GDATA(INDHEAT_G,'GDCOMB')=2 AND INDHEAT_GGCOMB(INDHEAT_GALIAS,INDHEAT_G) AND INDHEAT_GINGROUP(INDHEAT_GALIAS,INDHEAT_GROUP)), INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_GALIAS,INDHEAT_GROUP))
$label endcombtechQGFEQ
         ) ..
         INDHEAT_VGF_T(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)
         =e=
         SUM(INDHEAT_DHUSER$INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER),
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,IS3,T)/
         (
         INDHEAT_GDATA(INDHEAT_G,'GDFE')$(NOT INDHEAT_IGHP(INDHEAT_G))+INDHEAT_COP(INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)$(INDHEAT_IGHP(INDHEAT_G))
         )
         )
;

INDHEAT_QSUMPERCENT(IY411,INDHEAT_IA) ..
         SUM(INDHEAT_GROUP$(SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))), INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP))
         =e=
         1
;

INDHEAT_QMINPERCENT(IY411,INDHEAT_IA,INDHEAT_GROUP)$(SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))) ..
         INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP)
         =g=
         INDHEAT_MINPERCENT(IY411,INDHEAT_IA,INDHEAT_GROUP)*
         SUM(IY402$(IY411PREVY(IY411,IY402)),INDHEAT_VPERCENTGROUP(IY402,INDHEAT_IA,INDHEAT_GROUP))$(NOT IYFIRST(IY411))
         +SUM(INDHEAT_G$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP)),INDHEAT_GINSTALL(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))$(IYFIRST(IY411))
;

INDHEAT_QMAXPERCENT(IY411,INDHEAT_IA,INDHEAT_GROUP)$(INDHEAT_MAXPERCENT(IY411,INDHEAT_IA,INDHEAT_GROUP)) ..
         INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP)
         =L=
         INDHEAT_MAXPERCENT(IY411,INDHEAT_IA,INDHEAT_GROUP)
;

INDHEAT_QOUTEQIN(IY411,INDHEAT_IA,INDHEAT_G)$SUM((INDHEAT_GROUPFROM,INDHEAT_GROUPTO)$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUPTO),INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUPFROM)) ..
         SUM(INDHEAT_GROUP$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP),INDHEAT_VGKNMOVETO(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))
         =e=
         SUM(INDHEAT_GROUP$INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP),INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))
;

INDHEAT_QGMINPERCENT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$(INDHEAT_CARDGROUP(INDHEAT_GROUP)>1 AND INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)) ..
         SUM(INDHEAT_DHUSER$INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER),
         SUM((IS3,T), INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
         - SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO,IS3,T))
         )
         + SUM((INDHEAT_GALIAS,IS3,T)$(ORD(INDHEAT_GALIAS) NE ORD(INDHEAT_G) AND INDHEAT_GINGROUP(INDHEAT_GALIAS,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_GALIAS,INDHEAT_GROUP) AND INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL')),
           INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,IS3,T)
$ifi not %COMBTECH%==yes $goto endcombtechQGMINPERCENT1
           + SUM((INDHEAT_GALIAS2)$(INDHEAT_GDATA(INDHEAT_GALIAS2,'GDCOMB')=2 AND INDHEAT_GGCOMB(INDHEAT_GALIAS,INDHEAT_GALIAS2)),
$ifi not %HYDROGEN1%==yes $goto endhydrogenMINPERCENT1
         (1+INDHEAT_HYDROGEN_H2TOF(INDHEAT_GALIAS2)$(INDHEAT_GDATA(INDHEAT_GALIAS2,'GDFUEL') EQ THERMGAS_H2 OR INDHEAT_GDATA(INDHEAT_GALIAS2,'GDFUEL') EQ BIOGAS_H2))*
$label endhydrogenMINPERCENT1
           INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS2,INDHEAT_GROUP,IS3,T)
           )
$label endcombtechQGMINPERCENT1
         )
$ifi not %COMBTECH%==yes $goto endcombtechQGMINPERCENT2
         + SUM(INDHEAT_GALIAS$(INDHEAT_GDATA(INDHEAT_GALIAS,'GDCOMB')=2 AND INDHEAT_GGCOMB(INDHEAT_G,INDHEAT_GALIAS) AND INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)),
$ifi not %HYDROGEN1%==yes $goto endhydrogenMINPERCENT2
         (1+INDHEAT_HYDROGEN_H2TOF(INDHEAT_GALIAS)$(INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ THERMGAS_H2 OR INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ BIOGAS_H2))*
$label endhydrogenMINPERCENT2
           (SUM((IS3,T), INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
         - SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,INDHEAT_GROUPTO,IS3,T))
         ))
         )
$label endcombtechQGMINPERCENT2
         )
         =g=
         0.15*
         SUM(INDHEAT_DHUSER$INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER),
         (SUM((INDHEAT_GALIAS,IS3,T)$(INDHEAT_GINGROUP(INDHEAT_GALIAS,INDHEAT_GROUP) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_GALIAS,INDHEAT_GROUP)),
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,IS3,T)
         + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_GALIAS,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
         - SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_GALIAS,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,INDHEAT_GROUPTO,IS3,T))
$ifi not %COMBTECH%==yes $goto endcombtechQGMINPERCENT3
         + SUM(INDHEAT_GALIAS2$(INDHEAT_GDATA(INDHEAT_GALIAS2,'GDCOMB')=2 AND INDHEAT_GGCOMB(INDHEAT_GALIAS,INDHEAT_GALIAS2)),
$ifi not %HYDROGEN1%==yes $goto endhydrogenMINPERCENT3
         (1+INDHEAT_HYDROGEN_H2TOF(INDHEAT_GALIAS2)$(INDHEAT_GDATA(INDHEAT_GALIAS2,'GDFUEL') EQ THERMGAS_H2 OR INDHEAT_GDATA(INDHEAT_GALIAS2,'GDFUEL') EQ BIOGAS_H2))*
$label endhydrogenMINPERCENT3
                 (INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS2,INDHEAT_GROUP,IS3,T)
                 + SUM(INDHEAT_GROUPFROM$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUPFROM,INDHEAT_GROUP), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS2,INDHEAT_GROUPFROM, INDHEAT_GROUP,IS3,T))
                 - SUM(INDHEAT_GROUPTO$INDHEAT_GMOVE(INDHEAT_G,INDHEAT_GROUP,INDHEAT_GROUPTO), INDHEAT_VGHMOVE_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS2,INDHEAT_GROUP,INDHEAT_GROUPTO,IS3,T))
         ))
$label endcombtechQGMINPERCENT3
         )
         )
         )
;

INDHEAT_QSOLHDISPATCH(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP,IS3,T)$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP) AND SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)) AND INDHEAT_ISOLHSUMST(INDHEAT_IA) AND INDHEAT_GINGROUP(INDHEAT_IGSOLH,INDHEAT_GROUP))..
         SUM(INDHEAT_DHUSER$INDHEAT_GTODHUSER(INDHEAT_IGSOLH,INDHEAT_DHUSER),
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGSOLH,INDHEAT_GROUP,IS3,T)
         )
$ifi     %VRESHUTDOWN%==yes  =L=
$ifi not %VRESHUTDOWN%==yes  =E=

$ifi %SOLHFLH_DOL%==AAA       (INDHEAT_SOLHFLH(INDHEAT_IA) * (
$ifi %SOLHFLH_DOL%==AAA_GGG   (INDHEAT_SOLHFLH(INDHEAT_IA,INDHEAT_IGSOLH) * (
       INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)
         + INDHEAT_VGKNMOVETO(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)$(SUM(INDHEAT_GROUPFROM,INDHEAT_GMOVE(INDHEAT_IGSOLH,INDHEAT_GROUPFROM,INDHEAT_GROUP)))
         - INDHEAT_VGKNMOVEFROM(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)$(SUM(INDHEAT_GROUPTO,INDHEAT_GMOVE(INDHEAT_IGSOLH,INDHEAT_GROUP,INDHEAT_GROUPTO)))
         + INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_IGSOLH,INDHEAT_GROUP)
                            ) * INDHEAT_SOLH_VAR_T(INDHEAT_IA,IS3,T) * INDHEAT_IGKRATE(INDHEAT_IA,INDHEAT_IGSOLH,IS3,T))/ INDHEAT_ISOLHSUMST(INDHEAT_IA)
;

INDHEAT_QMINPERCENTSUM(IY411,INDHEAT_IA,INDHEAT_SUMGROUP)$INDHEAT_MINPERCENTREL(IY411,INDHEAT_IA,INDHEAT_SUMGROUP) ..
         SUM(INDHEAT_GROUP$(INDHEAT_RELGROUP(INDHEAT_SUMGROUP,INDHEAT_GROUP) AND SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))),INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP))
         =g=
         INDHEAT_MINPERCENTREL(IY411,INDHEAT_IA,INDHEAT_SUMGROUP)*
         SUM(INDHEAT_GROUP$(INDHEAT_RELGROUP(INDHEAT_SUMGROUP,INDHEAT_GROUP)
* AND SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))
         ),
         SUM(IY402$(IY411PREVY(IY411,IY402)),INDHEAT_VPERCENTGROUP(IY402,INDHEAT_IA,INDHEAT_GROUP))$(NOT IYFIRST(IY411))
         +SUM(INDHEAT_G$(INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP)),INDHEAT_GINSTALL(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))$(IYFIRST(IY411))
         )
;

INDHEAT_QMINPERCENTSUM2(IY411,INDHEAT_IA,INDHEAT_SUMGROUP)$INDHEAT_MINPERCENTREL2(IY411,INDHEAT_IA,INDHEAT_SUMGROUP) ..
         SUM(INDHEAT_GROUP$(INDHEAT_RELGROUP(INDHEAT_SUMGROUP,INDHEAT_GROUP) AND SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))),INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP))
         =g=
         INDHEAT_MINPERCENTREL2(IY411,INDHEAT_IA,INDHEAT_SUMGROUP)
;

INDHEAT_QMAXPERCENTSUM(IY411,INDHEAT_IA,INDHEAT_SUMGROUP)$INDHEAT_MAXPERCENTREL(IY411,INDHEAT_IA,INDHEAT_SUMGROUP) ..
         SUM(INDHEAT_GROUP$(INDHEAT_RELGROUP(INDHEAT_SUMGROUP,INDHEAT_GROUP) AND SUM(INDHEAT_G,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))),INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_GROUP))
         =l=
         INDHEAT_MAXPERCENTREL(IY411,INDHEAT_IA,INDHEAT_SUMGROUP)
;

INDHEAT_QHSTOVOLT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))..
    INDHEAT_VHSTOVOLT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)*(1-INDHEAT_GDATA(INDHEAT_IGHSTO,'GDSTOLOSS'))
  + WEIGHT_T(T)*CHRONOHOUR(IS3,T)*
   (SUM(INDHEAT_DHUSER,INDHEAT_VHSTOLOADT(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T))
  - SUM(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO), SUM(INDHEAT_DHUSER, INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)/INDHEAT_GDATA(INDHEAT_IGHSTO,'GDFE'))) )
    =E= INDHEAT_VHSTOVOLT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T++1)
;

INDHEAT_QHSTOLOADT(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))..
   SUM(INDHEAT_GALIAS$(INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_GALIAS,INDHEAT_IGROUPSTO) AND INDHEAT_GTODHUSER(INDHEAT_GALIAS,INDHEAT_DHUSER) AND INDHEAT_GINGROUP(INDHEAT_GALIAS,INDHEAT_IGROUPSTO) AND NOT INDHEAT_IGHSTO(INDHEAT_GALIAS)),
          INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_IGROUPSTO,IS3,T))
         =G=
   INDHEAT_VHSTOLOADT(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)
;

INDHEAT_QHSTOLOADTLIM(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)$((SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))
*$ifi %DECOM%==yes  OR IGDECOMEXOPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO)
                 ) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))..
     ((INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)
*$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM(IY411,INDHEAT_IA,INDHEAT_IGHSTO)$IGDECOMEXOPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO)
       )/INDHEAT_GDATA(INDHEAT_IGHSTO,'GDSTOHLOAD')
      + INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))/INDHEAT_GDATA(INDHEAT_IGHSTO,'GDSTOHLOAD')
     )*1
         =G=
   SUM(INDHEAT_DHUSER,INDHEAT_VHSTOLOADT(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T))
;

INDHEAT_QHSTOVGHTLIM(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)$((SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))
*$ifi %DECOM%==yes  OR IGDECOMEXOPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO)
                 ) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))..
     ((INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)
*$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM(IY411,INDHEAT_IA,INDHEAT_IGHSTO)$IGDECOMEXOPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO)
       )/INDHEAT_GDATA(INDHEAT_IGHSTO,'GDSTOHUNLD')
      + INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))/INDHEAT_GDATA(INDHEAT_IGHSTO,'GDSTOHUNLD')
     )*1
         =G=
     SUM(INDHEAT_DHUSER,INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T))
;

INDHEAT_QHSTOVOLTLIM(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)$((SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))
*$ifi %DECOM%==yes  OR IGDECOMEXOPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO)
                 ) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))..
     INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)
*$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM(IY411,INDHEAT_IA,INDHEAT_IGHSTO)$IGDECOMEXOPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO)
   + INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))
         =G=
  INDHEAT_VHSTOVOLT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)
;

INDHEAT_QGKSTO(IY411,INDHEAT_IA,INDHEAT_IGROUPSTO)..
         SUM(INDHEAT_IGHSTO$(INDHEAT_GINGROUP(INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)),
                 INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)$(SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)))
                 + INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)
         )
         =E=
         SUM(INDHEAT_G$(NOT INDHEAT_IGHSTO(INDHEAT_G) AND INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_IGROUPSTO)),
                 INDHEAT_VGKNACCUMNET(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_IGROUPSTO)$(SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),INDHEAT_IAGKNY(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_IGROUPSTO)))
                 + INDHEAT_GKFX(IY411,INDHEAT_IA,INDHEAT_G,INDHEAT_IGROUPSTO)
         )/INDHEAT_STOFACTOR(INDHEAT_IGROUPSTO)
;


INDHEAT_QHSTOGROUP(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGROUPSTO)$(SUM(INDHEAT_IGHSTO,INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)) ) ..
         SUM((INDHEAT_IGHSTO,IS3,T)$(INDHEAT_GINGROUP(INDHEAT_IGHSTO,INDHEAT_IGROUPSTO) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO)),
         INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T)*INDHEAT_GTODHUSER(INDHEAT_IGHSTO,INDHEAT_DHUSER))
         =L=
         INDHEAT_VPERCENTGROUP(IY411,INDHEAT_IA,INDHEAT_IGROUPSTO)*INDHEAT_DH(IY411,INDHEAT_IA,INDHEAT_DHUSER)
;

INDHEAT_QHSTOVGHSLIM(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3)$(INDHEAT_GTODHUSER(INDHEAT_IGHSTO,INDHEAT_DHUSER) AND INDHEAT_IAGK_HASORPOT(IY411,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO))..
   SUM(T,INDHEAT_VHSTOLOADT(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T))
         =G=
   SUM(T,INDHEAT_VGH_T(IY411,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_IGROUPSTO,IS3,T))
;
